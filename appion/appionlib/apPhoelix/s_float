#!/bin/csh -f

#####
#
# s_float : boxes image using top and bottom row average as float value
# Usage: s_float input output (colsize, for tmv method ONLY!)
# Input file: .j
# Output file: .j
#
#####

source phoelix_params

if ($s_verbose) echo `basename $0` $argv

set file=$1
set fileout=$2
set rowsize=$3

# extract the prefix of the file
set filep = $file:r
set filetemp=$filep.r$rowsize

# get the row size so that can excise last row
set frow=`hdim -r < $file`
set fcol=`hdim -c < $file`
if ($calling_method == am) then
    set newfcol=`po2 -f $fcol`
else if ($#argv == 4) then
    set newfcol = $4
else
    set newfcol = $fcol
endif

# excise row 1 and row n 
excise -r 1 < $file > row1
excise -r $frow < $file > rown
set av1=`avsd -m < row1`
set av2=`avsd -m <rown`
set av=`ee "[$av1 + $av2] : 2.0"`

pad -p $av -r $rowsize -c $newfcol < $file > $filetemp

# get the standard deviation 
set stdev=`avsd -s < $filetemp`

# bring to 0 mean with current standard deviation 
stdavsd -m 0.0 -s $stdev < $filetemp > $fileout 

rm $filetemp row1 rown
