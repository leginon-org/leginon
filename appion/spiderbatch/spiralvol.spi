[x20] ;[boxsize]
; spiralvol
; chiral 3d shape with 4 protruding spirals

FR
?OUT.Chiral spiral volume (dir/vol)?<vol> ;vol/chiralvol

; ~~~~~ start ~~~~~
x21=int(x20/3) ;length of cylinders
x22=int(x20/2)+1 ;center of box
x23=int(x20/50)+1 ;radius

x25=x23*2 ;sphere and helix radii
x26=x25*2 ;#spheres
MO 3
_2
x21,x21,x21 ;small box
H ; helix
(1) ; volume density
x25,x25 ; sphere and helix radii
x26,(1.0) ; #spheres,#turns

x25=int((x20-x21)/2)+1 ;x,z position
x26=x22-x21 ;ypos=center-spiralLength
PD
_2
_1
x20,x20,x20
M ;minimum
x25,x26,x25 ;topleft coordinates

RT 3D
_1
_2
(109.5),(0),(0)

RT 3D
_1
_3
(109.5),(120),(0)

RT 3D
_1
_4
(109.5),(240),(0)

;make central sphere
x25=x23*3 ;outer radius
x26=0 ;inner radius
MO 3 ;sphere on cylinder #2
_5
x20,x20,x20
SP ;sphere
N ;not from doc
(1) ;voxel density
x25,x26 ;outer inner
(x22,x22,x22) ;x,y,z
(0,0,0) ;end

;---combine cylinders
AD
_1 ;spiral#1
_2 ;spiral#2
_6 ;output
_3 ;spiral#3
_4 ;spiral#4
_5 ;central sphere
*

;---remove densities above 1 and below 0
TH
_6
<vol>
C  ;both sides
(1.0),(0)

DE
_1
DE
_2
DE
_3
DE
_4
DE
_5
DE
_6

RE
