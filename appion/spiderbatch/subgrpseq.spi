; subgrpseq 
; resequences averages and lists from multiple classifications (ie, cordiv)
; to facilitate subsequent superclassification
;
; input
; -list of classes (ie. view_count)
; -prefix from classification (ie. coran/cor)
; output
; -resequenced class averages (i*****)
; -resequenced class lists (d*****)
; -doc with newnum,div,class,#pcls (seqlist)

FR
?IN.LIST OF CLASSES (dir/doc)?<classlist> ;/view_count
FR
?IN.IMAGE LIST PREFIX (dir/prefix assumes ***_thrk and ***_avg)?<classprfx> ;/cor
FR 
?OUT.OUTPUT PREFIX (dir/pfx)?<outprfx> ;corbtm2/seq/ ; i@***** d***** seqlist

; ~~~~~ start ~~~~~
x99=0 ; subgroup counter
UD N x21
<classlist>
DO LB1 x11=1,x21 ; for each class

UD N x22
<classprfx>{***x11}_thrk
DO LB2 x12=1,x22 ; for each subclass
UD IC x12,x23,x24 ; number of particles in group
<classprfx>{***x11}_thrk

x99=x99+1
VM
mv <classprfx>{***x11}_thr{***x12}.$DATEXT <outprfx>d{*****x99}.$DATEXT

CP
<classprfx>{***x11}_avg{***x12}
<outprfx>i@{*****x99}

DE
<classprfx>{***x11}_avg{***x12}

SD x99,x99,x11,x12,x24 ;newnum,class,subclass,#pcls
<outprfx>seqlist

LB2 ; next subclass x12
UD ICE
<classprfx>{***x11}_thrk

LB1 ; next class x11

RE
