; mergesubclasslists
; after superclassification of class-averages
; will make particle membership docs for each superclass
; 
; input
; - superclass prefix
; - subclass prefix
;
; output
; - particle list doc for each superclass

FR
?IN.LIST OF SUPERCLASSES (dir/cor_thrk or _kmlist)?<superlist>
FR
?IN.SUPERCLASS PARTICLE LIST TEMPLATE (dir/cor_thrk or _km***)?<supertmpl>
FR
?IN.SUBCLASS PARTICLE LIST TEMPLATE (dir/thr***)?<subtmpl>
FR
?OUT.MERGED PREFIX (dir/cor assumes _mrglist and _mrg***)?<outpfx>

; ~~~~~ start ~~~~~

;for each superclass
UD N x21
<superlist>
x25=0    ; subclass counter
x28=0    ; particle counter

VM
echo Merging into {%F7.1%x21} classes <outpfx>'_mrg*** and _mrglist...'

SD IC NEW
mrglist_ic
(3),x21

DO LB1 x11=1,x21  ; for each superclass
UD IC x11,x22,x23 ;superclassnum,#subclasses
<superlist>

;for each subclass
UD N x24
<supertmpl>x22
IF(x23.ne.x24)THEN
 VM
 echo Error...number of subclasses in superclass {%F7.1%x22} does not agree
 EN
ENDIF

DOC COMBINE
<subtmpl>
<supertmpl>x22
<outpfx>_mrg{***x22}

UD N x27
<outpfx>_mrg{***x22}

x25=x25+x23 ; increment subclass counter
x28=x28+x27 ; increment particle counter

SD IC x11,x22,x23,x27 ;classnum,#subclasses,#particles
mrglist_ic

LB1 ; for each superclass x11
UD ICE
<superlist>

SD IC COPY
mrglist_ic
<outpfx>_mrglist
SD IC E
mrglist_ic

VM
echo -n ...from {%F7.1%x25} subclasses with 
;{%F8.3%x28}
VM
echo ' '{******x28} particles.

RE
