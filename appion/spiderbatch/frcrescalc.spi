[x23] ; pixel size (3.5,2.26,etc)
; resolution calculator
; outdoc has 0.5FRC and 3sigmaFRC
FR
?IN.FRC FILE (dir/doc)?<frcdoc>
FR
?OUT.RESOLUTION DOC (dir/doc)?<outdoc>

; ~~~~~ start ~~~~~

UD N x21
<frcdoc>
UD E

x22=0.5

DO LB1 x11=1,x21 ; first loop calculate 0.5-resolution

  UD IC x11,x31,x32,x33,x34,x35 ;freq,dph,frc,crt,pix
  <frcdoc>

  IF(x33.lt.x22)GOTO LB99

  x36=x31 ; radius above 0.5 = x1
  x37=x33 ; correlation above 0.5 = y1
LB1 ; next x11
LB99 ; escape loop 1
;VM
;echo 0.5 crit escaped on {**x11} loop

; interpolate crossing 0.5
;b=y1-m*x1
;m=((y1-y2)/(x1-x2))
;y3=m*x3+b 
; or x3=(y3-b)/m or x3=(y3-(y1-m*x1))/m or x3=((y3-y1)/m)+x1
IF(x36.eq.x31)THEN ;horizontal line has slope of zero
  VM
  echo freq1 is {%F7.3%x36}, freq2 is {%F7.3%x31}, frc1 is {%F7.3%x37}, frc2 is {%F7.3%x33}
ELSE
  x38=(x22-x37)/((x37-x33)/(x36-x31))+x36
ENDIF
x38=x38/x23 ; convert from 1/pix to 1/A
; do same calculation in A instead of 1/pix
x36=1/(x36/x23)
x31=1/(x31/x23)

DO LB2 x12=3,x21 ; second loop calculate 3-sigma-resolution
  ;first two points are first cross over find next = start loop at 3

  UD IC x12,x41,x42,x43,x44,x45
  <frcdoc>

  IF(x47.ge.x48)THEN ; frc was greater than 3-sigma
    IF(x43.lt.x44)GOTO LB98 ; frc is less than 3-sigma
  ENDIF

  x46=x41 ; x1-res
  x47=x43 ; y1-frc
  x48=x44 ; y1-crit
LB2 ; next x12
;if x12=x21 then frc never crossed 3-sigma
x53=0
GOTO LB97
LB98 ; escape loop2
;VM
;echo 3sigma escaped on {**x12} loop

; calculate point of intersection of frc and crit lines
; m1*x+b1=m2*x+b2
;  or x=(b2-b1)/(m1-m2)
IF(x46.eq.x41)THEN
  VM
  echo DIV BY ZERO...calculating intersection of 3-sigma and frc...check line {**x12} of frc doc
ENDIF
x49=(x48-x44)/(x46-x41) ; slope crit = m1
x50=(x47-x43)/(x46-x41) ; slope frc = m2
x51=x48-(x49*x46) ; intercept crit = b1
x52=x47-(x50*x46) ; intercept frc = b2
x53=(x52-x41)/(x49-x50) ; x-intersection of crit & frc lines
x53=x53/x23 ; convert from 1/pix to 1/A
LB97 ; 3sigma is infinite (x53=0)

x47=0 ; sum
x46=0 ; initial freq
DO LB3 x13=1,x21 ; third loop sums all information in frc above 3-sigma function
  UD IC x13,x41,x42,x43,x44,x45 ;freq,dph,frc,crt,pix
  <frcdoc>
  IF(x46.eq.0)GOTO LB96 ;first loop
  ;sum statictically significant information (above 3-sigma noise)
  IF(x43.gt.x44)x47=x47+((x43-x44)*(x41-x46)) ;area between frc and 3sigma
  LB96
  x46=x41
LB3 ; next x13

IF(x38.ne.0)x38=1/x38
IF(x53.ne.0)x53=1/x53
IF(x47.ne.0)x47=1/x47

SD /   0.5crit   3sigma     sum
<outdoc> 
SD 1,x38,x53,x47
<outdoc>
SD E
<outdoc>
UD ICE
<frcdoc>

VM
echo {%F7.1%x38}A is 0.5 crit and {%F7.1%x53}A is 3sigma crit for {%F7.1%x47}A total information

RE
