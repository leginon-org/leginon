; Description:
;   Window raw particles
; Input:
;   Particle coordinate files contain x,y in first two columns
; Output:
;   <outdir>/ser@ = Stack of raw (not normalized) windowed particles
;   <outdir>/serlist = List of particles, micro, and coordinate
FR
?IN.List of Micros (dir/miclist)?<miclist>
FR
?IN.Micros template (dir/mic****)?<mictmpl>
FR
?IN.Particle coordinates template (dir/cord****)?<cordtmpl>
RR x90
?IN.Decimation factor of particle coordinates (4)?
RR x23
?IN.Particle window size (50% larger than pcl radius)?
FR
?OUT.Output folder to make (dir)?<outdir>

; ~~~~~ start ~~~~~

VM
mkdir -p <outdir>

x24=x23 ;X and Y dimensions are same
; WINDOW THE PARTICLES FROM THE INPUT MICROGRAPH 
x33 = INT(x23/2)+1  ; nsam center
x34 = INT(x24/2)+1  ; nrow center
x70=0    ;"GLOBAL" PARTICLE COUNTER
x71=0    ;SKIPPED PARTICLE COUNTER

SD IC NEW
serlist_ic
(5,999999) ;ser,mic,x,y,pic

UD N x61
<miclist>
DO LB1 x11=1,x61  ; for each micro in microsdoc
  UD IC x11,x62
  <miclist>

  x99=0
  IQ FI x99
  <cordtmpl>x62
  IF(x99.eq.0)THEN
    VM
    echo ' *** 'Coordinate file does not exist for micro {%F7.1%x62}'***'
    GOTO LB90 ;skip to next micro
  ELSE
    VM
    echo -n Windowing particles from micro {%F7.1%x62}..
  ENDIF

  UD N x38
  <cordtmpl>x62
  x25=0               ;SET LOCAL PARTICLE COUNTER (KEYS)

  FS
  <mictmpl>x62
  FI x80,x81 ; find dimensions of micro
  <mictmpl>x62
  (12),(2)

  DO LB2 x12 = 1,x38 ;for each particle
    UD IC x12,x27,x28 ;web writes:x,y ;autopic writes: x y pcl# cc
    <cordtmpl>x62

    x82 = ((x27*x90) + x33) ; bottom right x
    x83 = ((x28*x90) + x34) ; bottom right y
    x84 = ((X27*x90) - x33) ; top left x
    x85 = ((X28*x90) - x34) ; top left y
    ; if beyond edge of micro then skip this particle
    IF(x82.gt.x80)GOTO LB50
    IF(x83.gt.x81)GOTO LB50
    IF(x84.lt.1)GOTO LB50
    IF(x85.lt.1)GOTO LB50

    x25=x25+1 ; increment local counter
    x70=x70+1 ; increment global counter

    WI
    <mictmpl>x62
    <outdir>/ser@{******x70}
    x23,x24
    x84,x85
    FS
    <outdir>/ser@{******x70}

    ;         ser,mic, x , y ,pic
    SD IC x70,x70,x62,x27,x28,x12
    serlist_ic ;<outdir>/serlist

    GOTO LB51 ;windowed successfully
      LB50 ; SKIPPED PARTICLE
      x71=x71+1 ; skipped particle counter
    LB51 ;windowed successfully
  LB2 ;next particle
  UD ICE
  <cordtmpl>x62

  VM
  echo .{%F7.1%x25} of {%F7.1%x38} particles windowed.

  LB90 ;skip to next micro
LB1 ;next micro
UD ICE
<miclist>

SD IC COPY
serlist_ic
<outdir>/serlist
SD IC END
serlist_ic

DE
_1
DE
_2
DE
_3
DE
_4
DE
_5

VM
echo {%F7.1%x71} particles excluded because windows extended outside their micros.
VM
echo {%F7.1%x70} particles windowed...saved stack as <outdir>/ser.$DATEXT

RE
