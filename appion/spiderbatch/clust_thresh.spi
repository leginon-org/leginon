(x21) ; Threshold
; Syntax: @clust_thresh(threshold)
; Description:
;  Hierarchical ascendant clustering after running CA S & CL HC
; CL HD makes a list of classes with number of members
;      <prefix>_thrk
; CL HE makes list of images in each class
;      <prefix>_thr***
; AS R makes class averages and variance
;      <prefix>_avg@*** and <prefix>_var@***

FR
?IN.Particles template (dir/ser@*****)?<imgtmpl>
FR
?IN.CorAn prefix (dir/cor assumes _dendoc)?<prefix>

; ~~~~~ start ~~~~~

CL HD
x21 ; THRESHOLD (0-1)
<prefix>_dendoc ; INPUT DENDROGRAM DOC FILE
<prefix>_thr    ; OUTPUT DOC (doc)

CL HE
x21 ; THRESHOLD (0-1)
<prefix>_dendoc  ; INPUT DENDROGRAM DOC FILE
<prefix>_thr*** ; OUTPUT DOC TEMPLATE (grp***)

; LOOP FOR EACH CLASS
UD N x41  ; how many classes at this threshold
<prefix>_thr
UD E

SD IC NEW
thrk
(2),x41

DO LB2 x11=1,x41
  UD IC x11,x42  ; how many images in this class
  <prefix>_thr

  SD IC x11,x11,x42 ; essentially doc key but that command doesn't work on cluster
  thrk

  AS R
  <imgtmpl> ; IMAGE TEMPLATE
  <prefix>_thr{***x11}     ; LIST OF IMAGES
  A     ; ALL IMAGES
  <prefix>_avg@{***x11} ; AVERAGE IMAGE
  <prefix>_var@{***x11} ; VARIANCE IMAGE

  ;LA ; this fails on yellowtail with environment variable error
  ;<prefix>_avg{***x11}
  ;<prefix>_avgla{***x11}
  ;{***x11}a{****x42}  ; class(Cmbtt)*** (a)***(images)
LB2

SD IC COPY
thrk
<prefix>_thrk
SD IC END
thrk
UD ICE
<prefix>_thr

DE
<prefix>_thr

VM
echo Threshold at {%ES11.3%x21} yields {%F7.1%x41} classes.

RE

