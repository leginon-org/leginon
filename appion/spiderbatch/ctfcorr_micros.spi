; This script will CTF correct your micrographs
FR
?IN.Prefix for your images (dir/image assumes 5digits*****)?<micpfx>
FR
?IN.Suffix (ma_2 or @001)?<sufx>
FR
?IN.Defocus doc (dir/ctf mic#,def,ast,azim)?<defdoc>
RR x40
?IN.Acceleration voltage or lambda (ex. 120 kV or 0.03349)?
RR x30
?IN.Pixel size (ex. 2.26 A/pix)?
RR x33
?IN.Source size (try 0.005 1/A)?
RR x34
?IN.Defocus spread (try 0 A)?
RR x31
?IN.Amplidude contrast ratio (typically 0.1-0.05, try 0.09)?
RR x32
?IN.Gaussian envelope halfwidth (typically 0.1-0.05, try 0.1 1/A)?
RR x90
?IN.TF CT or TF C (1 or 2)?

; ~~~~~ start script ~~~~~
MD ;use all processors
SET MP
(0)

x30=0.5/x30 ;max spatial frequency=1/(2*Apix)

;find lambda constant from kV
IF(x40.eq.100)x41=0.03701
IF(x40.eq.120)x41=0.03349
IF(x40.eq.140)x41=0.03074
IF(x40.eq.160)x41=0.02851
IF(x40.eq.180)x41=0.02665
IF(x40.eq.200)x41=0.02508
IF(x40.eq.300)x41=0.01969
IF(x40.eq.400)x41=0.01644
IF(x40.lt.1)x41=x40 ;then lambda constant was supplied

UD N x20 ;how many lines in document
<defdoc>
DO LB1 x11=1,x20 ;for each image
  UD IC x11,x25,x21,x26,x27 ;open document, read line x11, store value in x21
  <defdoc>

  ;  x21 in microns, need Angstroms
  x22=x21 ;*10000.

  VM ;enter shell command
  echo -n CTF correcting <micpfx>{*****x25}<sufx> ..

  FT ;FT image
  <micpfx>{*****x25}<sufx>
  _2

  FS ;calculate statistics for image
  <micpfx>{*****x25}<sufx>
  FI x23,x24  ;find dimensions
  <micpfx>{*****x25}<sufx>
  (12,2) ;columns,rows

  IF(x90.eq.1)THEN
  TF CT   ;compute tranfer function in complex form compatible with the Fourier transform format
  _3           ;output file
  (2.0)        ;CS spherical aberration constant (mm)
  (x22,x41)    ;defocus (A), lambda (A)
  (x23,x24)    ;xsize,ysize (pix)
  (x30)        ;max spatial frequency (1/A)
  (x33,x34)    ;source size (1/A), defocus spread (A)
  (x26,x27)    ;astigmatism,azimuth
  (x31,x32)    ;amplitude contrast ratio (0-1), gaussian envelope halfwidth (1/A)
  (-1)         ;-1 for underfocus
  ENDIF
  IF(x90.eq.2)THEN
  TF C   ;compute tranfer function in complex form compatible with the Fourier transform format
  _3           ;output file
  (2.0)        ;CS spherical aberration constant (mm)
  (x22,x41)    ;defocus (A), lambda (A)
  (x23,x24)    ;xsize,ysize (pix)
  (x30)        ;max spatial frequency (1/A)
  (x33,x34)    ;source size (1/A), defocus spread (A)
  (x26,x27)    ;astigmatism,azimuth
  (x31,x32)    ;amplitude contrast ratio (0-1), gaussian envelope halfwidth (1/A)
  (-1)         ;-1 for underfocus
  ENDIF
;SOURCE SIZE[1/A]:size of source in the back focal plane of the objective lens. Small value = high coherence
;DEFOCUS SPREAD[A]:magnitude of defocus variations due to energy spread and lens current fluctuations

  MU ;multiply
  _2 ;FTimage
  _3 ;transfer function
  _1 ;corrected FTimage
  *  ;end
  
  FT ;FT
  _1 ;corrected FTimage
  <micpfx>ctf{*****x25} ;final 2D CTF corrected image

  VM ;enter shell command
  echo .<micpfx>ctf{*****x25} written.
LB1
UD ICE ;close open document
<defdoc>

RE
