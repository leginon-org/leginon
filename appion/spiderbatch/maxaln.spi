[x21,x22] ;maxrt,maxsh
; maxaln ;alignment threshold
;  applies specified rotation and shift limits to particle alignment
;  one of two options if limit exceeded:
;   0) if any limit is exceeded return parameters to 0,0,0
;   1) if a limit is exceeded restrict that parameter particle to limit

FR
?IN.SAP alignment parameter doc (dir/doc)?[sapdoc]
FR
?IN.Method (reset=0,restrict=1)?[method]
FR
?OUT.SAP alignment parameter doc (dir/doc)?[outdoc]

; ~~~~~ start ~~~~~
x20=[method]

UD N x23,x24 ;keys,regs
[sapdoc]
IF(x24.eq.4)GOTO LB20 ;oldsap = pcl,rt,xsh,ysh
IF(x24.eq.5)GOTO LB20 ;newsap = pcl,rt,xsh,ysh,grp
  VM
  echo Alignment doc is not SAP format
  EN
LB20

SD IC NEW
thresholdalignment_ic
x24,x23 ;regs,keys

VM
echo -n Thresholding alignment parameters..

x40=0 ;misalign counter
DO LB1 x11=1,x23 ;for each alignment param
  x41=0 ;misalign indicator
  IF(x24.eq.4)THEN
    UD IC x11,x31,x32,x33,x34
    [sapdoc]
  ELSE
    UD IC x11,x31,x32,x33,x34,x35
    [sapdoc]
  ENDIF

  IF(x32.gt.180)x36=x32-360 ;angles greater than 180 are negative

  IF(x20.eq.0)THEN ;reset parameters
    x30=abs(x36)
    IF(x30.gt.x21)GOTO LB21 ;rotation limit exceeded
    x30=abs(x33)
    IF(x30.gt.x22)GOTO LB21 ;xshift limit exceeded
    x30=abs(x34)
    IF(x30.gt.x22)GOTO LB21 ;yshift limit exceeded
    GOTO LB22 ;within limits
    LB21 ;at least one parameter exceeded limits
      x32=0
      x33=0
      x34=0
      x41=1 ;misalign indicator
    LB22
  ELSE ;IF(x20.eq.1)THEN ;restrict any overlimit paramter - keep sign
    x30=abs(x36)
    IF(x30.gt.x21)THEN
      x32=x21*x36/abs(x36)
      x41=1 ;misalign indicator
      IF(x32.lt.0)x32=x32+360 ;
    ENDIF
    x30=abs(x33)
    IF(x30.gt.x22)THEN
      x33=x22*x33/abs(x33)
      x41=1 ;misalign indicator
    ENDIF
    x30=abs(x34)
    IF(x30.gt.x22)THEN
      x34=x22*x34/abs(x34)
      x41=1 ;misalign indicator
    ENDIF
  ENDIF

  IF(x24.eq.4)THEN ;oldsap format
    SD IC x11,x31,x32,x33,x34
    thresholdalignment_ic
  ELSE ;newsap format
    SD IC x11,x31,x32,x33,x34,x35
    thresholdalignment_ic
  ENDIF
  x40=x40+x41
LB1 ;next alignment param
UD ICE
[sapdoc]
SD IC COPY
thresholdalignment_ic
[outdoc]
SD IC END
thresholdalignment_ic

IF(x20.eq.0)THEN
  VM
  echo {%F9.1%x40} of {%F9.1%x23} particles reset.
ELSE
  VM
  echo {%F9.1%x40} of {%F9.1%x23} particles restricted.
ENDIF

RE
