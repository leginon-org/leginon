; Reference Free Alignment Procedure
; version 20090115

FR L    ;list of particles to align
[pcllist]iral2_50/serkeeplist
FR L    ;particle template
[pcltmpl]iser2/mfiser@******
FR L    ;output directory for initial 10xAPSR
[alndir]ifal3
x26=10   ;number of apsrrounds
x21=27   ;radius for alignment (pix)
x24=4.2  ;A/pix of particles

;Will use newref if it exists. 
;Otherwise will resize refimg to make newref
;If refimg doesn't exist then will make one from apsraverages
FR L    ;reference image to orient alignment
[refimg]/home/brignole/spifiles/ksq_shmrav
x27=2.26  ; A/pix of refimg
FR L    ;resized refimage
[newref]ksq_ipshmrav

; ~~~~~ let the fun begin ~~~~~

@fraln[x21,x26] ;radius,rounds
[pcllist]
[pcltmpl]
*
[alndir]

x99=0
IQ FI x99
[newref]
IF(x99.eq.1)GOTO LB90 ;newref exists - skip ahead

x99=0
IQ FI x99
[refimg]
IF(x99.eq.0)THEN ;refimg does not exist either
  @alnapsr[x21]
  [alndir]
  [pcllist]
  (1) ;align averages without reference
  GOTO LB91 ;skip alignment to reference below
ENDIF

IF(x27.eq.x24)THEN ; apix(ref)=apix2(pcls)
  CP
  [refimg]
  [newref]
ELSE
  ;get size of refimg
  FS
  [refimg]
  FI x33
  [refimg]
  (12) ; nsam

  ;get size of particles
  UD N x20
  [pcllist]
  UD IC x20,x33
  [pcllist]
  FS
  [pcltmpl]x33
  FI x25
  [pcltmpl]x33
  (12)
  x32=x27/x24 ;scale factor
  x32=int(x33*x32)+1  ;scaled image size

  @resizeimg[x27,x24,x25] ;inApix,outApix,Size
  [refimg]
  [newref]
ENDIF

LB90 ; skip to here if newref already exists

@alnapsr[x21]
[alndir]
[pcllist]
(3) ; align avgs, orient to ref, then use avg(aligned avgs) to realign avgs
[newref]

LB91 ; skip to here if no reference exists

@apsrstat
[alndir]
[alndir]/apsrsum
[alndir]/apsrerr

DE
[alndir]/apsrsum

VM
echo Now choose the best APSR round - apply transformation and do classification.

EN D
