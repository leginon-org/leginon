; cleanParticleList (20090118)
;Description:
; removes particles that are in rejected groups 
; for a specified number of rounds of iterative alignment
;Input:
; list of rejected classes
; alignment directory containing r**/grp*** particle lists
; number of alignment rounds that a particle is in a bad class to be blacklisted
; list of aligned particles
;Output:
; cleaned list of particles

FR
?IN.List of rejected classes (dir/list)?[badgrplist]
FR
?IN.Template for first set of groups (dir/grp***)?[grptmpl1]
FR
?IN.Template for second set of groups (dir/grp***)?[grptmpl2]
FR
?IN.List of aligned particles (dir/serlist)?[pcllist]
FR
?OUT.Cleaned particle list (dir/cleanlist)?[newlist]

; ~~~~~ start ~~~~~
;RR x20
;?IN.Number of consecutive alignment rounds (2)?
x20=2

UD N x21
[badgrplist]
UD E

VM
echo Removing particles from {%F7.1%x21} bad classes...

;VM
;ls -1d [alndir]/r* | wc -l | nl -s '  1  ' > [newlist]_tmpnum.spi
;UD 1,x30
;[newlist]tmpnum
;UD E
x30=20

DOC COMBINE
[grptmpl1]  ;[alndir]/r{**30}/grp***
[badgrplist]
[newlist]tmp1

UD N x22
[newlist]tmp1
UD E
VM
echo First groups have {%F7.1%x22} bad particles.

x31=x30
IF(x20.eq.1)GOTO LB90

x29=x20-1
DO LB1 x11=1,x29
  x31=x30-x11
  x32=x32+1

  DOC COMBINE
  [grptmpl2] ;[alndir]/r{**31}/grp***
  [badgrplist]
  [newlist]tmpcomb

  UD N x23
  [newlist]tmpcomb
  UD E
  VM
  echo -n Second groups have {%F7.1%x23} bad particles..

  ;particles that were in bad groups again
  DOC AND
  [newlist]tmp1   ;1st file
  [newlist]tmpcomb  ;2nd file
  [newlist]tmp2   ;out file
  (1)               ;column to compare

  UD N x21
  [newlist]tmp2
  UD E

  IF(x23.lt.x22)x22=x23 ;x22=lesser of(x22,x23)
  x24=x21/x22*100
  x22=x21 ;store minimum number of particles for next round

  VM
  echo '.with '{%F7.1%x21}' ('{%F7.2%x24}'% ) overlap.'
LB1

LB90
DOC SUBTRACT
[pcllist]
[newlist]tmp2
[newlist]tmp
(1) ;first column

DOC REN
[newlist]tmp
[newlist]

UD N x25
[newlist]
UD E

VM
echo Bad particles were subtracted from [pcllist] to make list of {%F7.1%x25} good particles.

VM
rm -f [newlist]tmp*.$DATEXT

RE

