;jumpers
; makes a list of particles that have jumped from one alignment round to the next.

FR
?IN.List of classes to compare (dir/list)?<list>
FR
?IN.Template for first set of classes (dir/grp***)?<tmpl1>
FR
?IN.Template for second set of classes (dir/grp***)?<tmpl2>
FR
?OUT.List of jumping particles (dir/jumplist)?<outlist>

; ~~~~~ start ~~~~~
UD N x21
<list>
DO LB1 x11=1,x21 ;foreach grplist
  UD IC x11,x31
  <list>

  x90=0
  IQ FI x90  ;does grp*** exist in first round
  <tmpl1>x31
  x91=0
  IQ FI x91  ;does grp*** exist in 2nd round
  <tmpl2>x31
  
  x92=x90+x91
  IF(x92.eq.2)THEN  ;if particle list was present in both first and 2nd round
    UD N x32
    <tmpl1>x31
    UD E
    UD N x33
    <tmpl2>x31
    UD E
    
    DOC SUBTRACT    ;subtract 2nd list from first list
    <tmpl1>x31
    <tmpl2>x31
    <outlist>_tmp{***x31}
    (1)
    GOTO LB90
  ENDIF
  
  IF(x90.eq.1)THEN ;grp only existed in first round
    VM ;this x31 string substitution fails sometimes in Spider13
    cp <tmpl1>x31.$DATEXT <outlist>_tmp{***x31}.$DATEXT
  ENDIF
  
  IF(x91.eq.1)THEN ;grp only existed in 2nd round
    VM ;this x31 string substitution fails sometimes in Spider13
    cp <tmpl2>x31.$DATEXT <outlist>_tmp{***x31}.$DATEXT
  ENDIF

  LB90
  ;UD N x34
  ;<outlist>_tmp{***x31}
  ;UD E
  ;x34 particles have jumped from group x31 
LB1
UD ICE
<list>

DOC COMBINE
<outlist>_tmp***
<list>
<outlist>

UD N x34
<outlist>
UD E

VM
echo {%F7.1%x34} particles have jumped to a new class.

VM
rm -f <outlist>_tmp*

RE
