(x30,x31,x32) ; lowpass(A),highpass(A),pixsize(A/pix)
; syntax: @filter(x30,x31,x32)
; description: low and high pass butterworth filters particles
;   50% low pass is 2/3*lowpass (eg. 18 A will to taper to half at 12 A)
;   50% high pass is 4/3*highpass (eg. 300 A will taper to half at 400 A)
;
; enter 0 to ignore high or low pass filter

FR
?IN.List of images (dir/doc)?<pcllist>
FR
?IN.Images template (dir/ser@****)?<pcltmpl>
FR
?OUT.Filtered images template (dir/fser@****)?<outtmpl>

; ~~~~~ start ~~~~~

UD N x23
<pcllist>

VM
echo -n Filtering {%F7.1%x23} particles.

;lowpass x30 to x56
x56=x30*2/3  ;cutoff frequency has greater (1/A) = smaller (A)

;highpass x57 to x31
x57=x31*4/3  ;pass frequency has smaller (1/A) = greater (A)

IF(x56.le.0)GOTO LB90 ; highpass only

IF(x57.le.0)GOTO LB91 ; lowpass only

;----- do both filters: low pass then high pass -----
VM
echo -n .{%F7.1%x30} to {%F7.1%x56} A and {%F7.1%x57} to {%F7.1%x31} A..

IF(x30.ge.x31)THEN
  VM
  echo ERROR: Low pass is higher than high pass!
  EN
ENDIF

x65=x32/x30
x66=x32/x56
x67=x32/x57
x68=x32/x31

DO LB1 x11=1,x23
  UD IC x11,x22
  <pcllist>

  ;low pass
  FQ
  <pcltmpl>x22
  _1
  (7)        ;Butterworth low pass filter   
  x65,x66    ;passing and cut-off frequency (in 1/pixel)

  ;high pass
  FQ
  _1
  <outtmpl>x22
  (8)        ;Butterworth high pass filter
  x67,x68    ;starting and ending frequency
LB1
UD ICE
<pcllist>

GOTO LB99 ; exit

LB90 ;highpass only
;----- do high pass only -----
VM
echo -n .{%F7.1%x57} to {%F7.1%x31} A..

x67=x32/x57
x68=x32/x31
DO LB2 x11=1,x23
  UD IC x11,x22
  <pcllist>

  ;high pass
  FQ
  <pcltmpl>x22
  <outtmpl>x22
  (8)        ;Butterworth high pass filter
  x67,x68    ;starting and ending frequency
LB2
UD ICE
<pcllist>

GOTO LB99 ; exit

LB91 ; lowpass only
;----- do low pass only -----
VM
echo -n at {%F7.1%x30} to {%F7.1%x56} A...

x65=x32/x30
x66=x32/x56
DO LB3 x11=1,x23
  UD IC x11,x22
  <pcllist>

  ;low pass
  FQ
  <pcltmpl>x22
  <outtmpl>x22
  (7)         ;Butterworth low pass filter   
  x65,x66     ;starting and ending frequency (in 1/pixel) for filtering
LB3
UD ICE
<pcllist>

LB99 ; exit

DE
_1

VM
echo done.

RE
