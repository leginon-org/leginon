; angle fix for micropairs
FR
?IN.Categorized micro list with 1=OK, 2=rotate 180 (mic/orient)?<catdoc>
FR
?IN.Angles doc template (mics/dcb***)?<dcbtmpl>
RR x90
?IN.Factor to subtract from micro to get angles doc (1000)?
FR
?IN.Corrected angles doc template, factor not subtracted (mics/dcc****)?<dcctmpl>

; ~~~~~ start ~~~~~
UD N x21
<catdoc>
UD E

DOC SORT
<catdoc>
anglefix_tmp
(1)
Y

x20=x21/2-int(x21/2)
IF(x20.ne.0)THEN
  VM
  echo ERROR...Odd number of micros..{%F7.1%x21}. This only works with micro pairs.
  EN
ENDIF

x21=x21/2
DO LB1 x11=1,x21 for each categorized micro
  x20=(x11*2)-1
  UD IC x20,x22,x23
  anglefix_tmp
  
  x24=x22-x90
  SD IC NEW
  cor_ic
  (6,4)
  DOC REN
  <dcbtmpl>x24
  <dcctmpl>x22
  DO LB2 x12=1,3 ;first three rows of dcb
    UD IC x12,x31,x32,x33,x34,x35,x36
    <dcctmpl>x22
    SD IC x12,x31,x32,x33,x34,x35,x36
    cor_ic
  LB2 ;next row of dcb

  x12=4
  UD IC x12,x31,x32,x33,x34,x35,x36
  <dcctmpl>x22
  UD ICE
  <dcctmpl>x22
  DE
  <dcctmpl>x22
  IF(x23.eq.2)THEN
    x32=x32+180
    x33=x33+180
  ENDIF
  SD IC x12,x31,x32,x33,x34,x35,x36
  cor_ic
  SD IC COPY
  cor_ic
  <dcctmpl>x22
  SD IC END
  cor_ic
LB1 ;next micro
UD ICE
anglefix_tmp
DE
anglefix_tmp

RE
