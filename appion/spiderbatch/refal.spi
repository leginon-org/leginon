[x80] ; particle radius
; refal: reference alignment with mirrors using AP SH
;
; output: apsh_doc
; after this call refgrp.spi to output class averages and class membership
;
FR
?IN.LIST OF PARTICLES TO BE ALIGNED (dir/file)?<pcllist>
FR
?IN.PARTICLES TEMPLATE (dir/ser*****)?<pcltmpl>
FR
?IN.LIST OF REFERENCE IMAGES (dir/file)?<reflist>
FR
?IN.REFERENCE TEMPLATE (dir/tmp***)?<reftmpl>
FR
?OUT.MAKE THIS OUTPUT FOLDER FIRST (dir)?<outdir>

; ~~~~~ start ~~~~~
; HOW MANY PARTICLES
UD N x81
<pcllist>
VM
echo -n Aligning {%F7.1%x81} particles' '
UD N x85
<reflist>
VM
echo -n to {%F7.1%x85} references

; CHECK DIMENSIONS OF FIRST PARTICLE AND REFERENCE
UD 1,x84
<pcllist>
FS
<pcltmpl>x84
FI x82,x83
<pcltmpl>x84
12,2

UD 1,x86
<reflist>
FS
<reftmpl>x86
FI x87,x88
<reftmpl>x86
12,2

IF(x87.ne.x82)THEN
VM
echo Particles are {%F7.1%x87} NROW but References are {%F7.1%x82} NROW
RE
ENDIF
IF(x88.ne.x83)THEN
VM
echo Particles are {%F7.1%x88} NSAM but References are {%F7.1%x83} NSAM
RE
ENDIF

; set inner radius ; resize outer if necessary
x79=5 ; inner radius
IF(x80.le.20)x79=3
IF(x80.le.10)x79=1
IF(x80.le.5)THEN
 VM
 echo Error...Your radius {%F7.1%x80} is too small...using half image instead.
 x80=(x82/2)-2  ; extend radius to 1/2 image size
 IF(x80.le.5)THEN
  VM
  echo Error...Your images are too small = {%F7.1%x82} pixels.
  RE
 ENDIF
ENDIF

x84=((x82/2)-x80-2)   ; search range = image radius - particle radius - 2
IF(x84.lt.0)x84=0     ; search range must be nonnegative

; ALIGN PROJECTIONS AND PARTICLES
VM
echo ..Radius {%F7.1%x79} to {%F7.1%x80}, Translation {%F7.1%x84}

x89=0
SD IC NEW
refangles_ic
3,x85
DO LB2 x12=1,x85
  UD IC x12,x13
  <reflist>
  SD x13,x89,x89,x89 ; phi theta psi are zero
  <outdir>/refangles_tmp
LB2
UD IC END
<reflist>
SD E
<outdir>/refangles_tmp

AP SH
<reftmpl>      ; REFERENCE PROJECTIONS TEMPLATE
<reflist>      ; SELECTION OF REFERENCE PROJECTIONS OR DOC
x84,1          ; TRANSLATION RANGE AND STEP SIZE
x79,x80        ; FIRST AND LAST RING
<outdir>/refangles_tmp   ; EULER ANGLES OF REFIMG
<pcltmpl>      ; PARTICLES TEMPLATE
<pcllist>      ; SELECTION OF PARTICLES OR DOC
*          ; prior alignment doc
(0,0)      ; no angle range and threshold
(1)        ; mirror
<outdir>/apsh_doc ; ALIGNMENT OUTPUT DOC

DE
<outdir>/refangles_tmp

RE
