[x91] ; radius
; maskrefaligned.spi
; 1) mask avg*** and avg+mr
; 2) align maskav*** to maskavgmr
; 3) sap alignment parameters, rotate and mask individual particles
; output
;  rtpma@****** = stack of aligned masked particles
;  centerdoc = doc containing rt,x,y to center mask
;  alnavgdoc = doc for aligning averages
;  apshrtpdoc, apnqrtpdoc, saprtpdoc - new particle alignment adjusted for alignment of class averages
; THEN free-align masked particles

FR
?IN.MASK (dir/mask)?<mask>
FR
?IN.LIST OF CLASSES TO MASK (dir/classlist)?<grplist>
FR
?IN.CLASS AVERAGE TEMPLATE (dir/avg***)?<avgtmpl>
FR
?IN.UNALIGNED PARTICLE TEMPLATE (dir/ser******)?<pcltmpl>
FR
?IN.ALIGNMENT DOC (dir/doc)?<alndoc>
FR
?IN.ALIGNMENT FORMAT (1=apsh,2=apnq,3=apsr,4=sap)?<alntype>
x20=<alntype>
IF(x20.eq.3)THEN ; if apsrformat then ask for corresponding particle list
FR
?IN.PARTICLE LIST THAT CORRESPONDS WITH APSR DOC (dir/list)?<apsrlist>
ENDIF
FR
?OUT.EXISTING OUTPUT DIRECTORY (dir)?<outdir>

; ~~~~~ start ~~~~~

; initialize image stack
;DOC SORT
;<grplist>
;<outdir>/grplistsort
;(1)
;Y
;UD N x24
;<outdir>/grplistsort
;UD x24,x25
;<outdir>/grplistsort
;FS
;<avgtmpl>x25
;FI x26,x27
;<avgtmpl>x25
;(12),(2)
;MS I
;_5@
;x26,x27 ; nsam,nrow
;x24     ; num images
;x25     ; highest image
;DE
;<outdir>/grplistsort

;---mask and center averages---
@/home/brignole/scripts/maskshift
<grplist>
<avgtmpl>
<mask>
<outdir>/maskav@***** ;_5@*****
<outdir>/centerdoc

;---mask and center avg of averages---
AS R
<avgtmpl>
<grplist>
A
_1 ;avg
_2 ;var

MR
_1 ;avg
_2 ;avgmr
Y  ;yaxis

AS R
_*
(1-2)
A
_3 ;avg+mr
_4 ;var

MU
<mask>
_3 ;avg+mr
_1 ;maskavg+mr
*

UD 1,x57,x58,x59
<outdir>/centerdoc
SH
_1 ; maskavg+mr
<outdir>/maskavgmr ; maskavg+mr_sh
x58,x59

; invert for uncentering
x60=-x57
x61=-x58
x62=-x59
;SD 1,x60,x61,x62
;<outdir>/uncenterdoc
;---align masked,centered-averages to masked,centered-avg of averages---
@/home/brignole/scripts/alnavg[x91]
<outdir>/maskav@*****   ; _5@***** ;averages template
<grplist>   ; list of averages
(3)         ; refalign
<outdir>/maskavgmr   ; refimage maskavg+mr_sh
<outdir>/alnavg      ; prefix output: alnavgdoc alnavgav alnavgva alnavg@

DE   ; delete image stack
<outdir>/alnavg
DE
<outdir>/alnavgav
DE
<outdir>/alnavgva
DE
<outdir>/maskav  ;_5
;@/home/brignole/scripts/deletelist
;<outdir>/maskav***
;<grplist>
;---sap parameters, rotate and mask particles
UD N x21
<outdir>/alnavgdoc
SD IC NEW
alnavgdoctemp
6,x21

DO LB1 x11=1,x21 ; for each class
UD IC x11,x31,x32,x33,x34
<outdir>/alnavgdoc

SA P x32,x33,x34 ; shift+align
x57,x58,x59 ; shift
x32,x33,x34 ; align

SA P x32,x33,x34  ; shift+align+unshift
x32,x33,x34 ; shift+align
x60,x61,x62 ; unshift

SD IC x11,x31,x32,x33,x34
alnavgdoctemp

LB1 ; next x11
UD ICE
<outdir>/alnavgdoc
SD IC COPY ; overwrite old alignment
alnavgdoctemp
<outdir>/alnavgdoc
SD IC END
alnavgdoctemp

;---apply class alignment to individual particles
VM
echo Combining class alignment with individual particle alignment..
DOC SORT
<outdir>/alnavgdoc  ; sap format
<outdir>/alnavgsort ; sort sap format
(1)
Y
UD N x23
<outdir>/alnavgsort

UD N x22
<alndoc>

x40=1 ; start with group 1
x30=0 ; found particle counter
;;;;; apsh module
IF(x20.eq.1)THEN ; apsh
SD IC NEW
incoreapshdoc
15,x22

DOC SORT
<alndoc>
<outdir>/tempapshdoc
(4)  ; reference group in column 4
Y

DO LB2 x12=1,x22 ; for each particle
UD IC x12,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
<outdir>/tempapshdoc
; x44 is ref, x45 is pcl, x46-8 is alignment

DO LB3 x13=x40,x23 ; for each group
UD IC x13,x31,x32,x33,x34
<outdir>/alnavgsort

IF(x44.eq.x31)THEN ; found group
x30=x30+1 ; found particle counter
x40=x13; start from this group next round
SA P x37,x38,x39 ; apsh+apnq
x46,x47,x48    ;apsh
x32,x33,x34    ;sap

SD IC x12,x41,x42,x43,x44,x45,x37,x38,x39,x49,x50,x51,x37,x38,x39,x55
incoreapshdoc
GOTO LB90
ENDIF

LB3 ; next group x13

; group alignment not found for this particle, so just resave it
SD IC x12,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
incoreapshdoc
LB90 ; found group so skip to here

RT SQ
<pcltmpl>x45
_1
x37
x38,x39

MU
_1
<mask>
_2
*

SH
_2
<outdir>/rtpma@{******x45}
x58,x59

LB2 ; next particle x12
UD ICE
<outdir>/alnavgsort
DE
<outdir>/alnavgsort
UD ICE
<outdir>/tempapshdoc
DE
<outdir>/tempapshdoc

SD IC COPY
incoreapshdoc
<outdir>/apshrtpdoc
SD IC END
incoreapshdoc

ENDIF ; apsh
;--- end APSH module - start APNQ module ---
IF(x20.eq.2)THEN ; apnq
SD IC NEW
incoreapnqdoc
6,x22

DOC SORT
<alndoc>
<outdir>/tempapnqdoc
(1) ; referencegroup in column 1
Y

DO LB4 x12=1,x22 ; for each particle
UD IC x12,x41,x42,x43,x44,x45,x46 
<outdir>/tempapnqdoc
; x41=ref,x42=cc,x43-5=aln;x46=pcl

DO LB5 x13=x40,x23 ; for each group
UD IC x13,x31,x32,x33,x34
<outdir>/alnavgsort

IF(x41.eq.x31)THEN ; found group
x30=x30+1 ; found particle counter
x40=x13 ; start from this group next round
SA P x37,x38,x39
x43,x44,x45
x32,x33,x34

SD IC x12,x41,x42,x37,x38,x39,x46 
incoreapnqdoc
GOTO LB91
ENDIF

LB5 ; next group x13

; group alignment not found for this particle, so ignore it 
SD IC x12,x41,x42,x43,x44,x45,x46
incoreapnqdoc
LB91 ; found group so skip to here

RT SQ
<pcltmpl>x46  ; particle x46
_1
x37
x38,x39

MU
_1
<mask>
_2
*

SH
_2  ; input
<outdir>/rtpma@{******x46} ; output
x58,x59

LB4 ; next particle x12
UD ICE
<outdir>/alnavgsort
DE
<outdir>/alnavgsort
UD ICE
<outdir>/tempapnqdoc
DE
<outdir>/tempapnqdoc

SD IC COPY
incoreapnqdoc
<outdir>/apnqrtpdoc
SD IC END
incoreapnqdoc

ENDIF ; apnq
;--- end APNQ module - start APSR/SAP module ---
IF(x20.gt.3)THEN ; APSR OR SAP
SD IC NEW
incoresapdoc
4,x22

IF(x20.eq.3)THEN ; apsr
@/home/brignole/scripts/apsr2sap
<apsrlist>
<alndoc>
<outdir>/tempapsrdoc
DOC SORT
<outdir>/tempapsrdoc
<outdir>/tempsapdoc
(1)
Y
DE
<outdir>/tempapsrdoc
ENDIF

IF(x20.eq.4)THEN ; sap
DOC SORT
<alndoc>
<outdir>/tempsapdoc
(1) ; referencegroup in column 1
Y
ENDIF

DO LB6 x12=1,x22 ; for each particle
UD IC x12,x46,x43,x44,x45 
<outdir>/tempsapdoc
; x46=pcl,x43-5=aln;

DO LB7 x13=x40,x23 ; for each group
UD IC x13,x31,x32,x33,x34
<outdir>/alnavgsort

IF(x41.eq.x31)THEN ; found group
x30=x30+1 ; found particle counter
x40=x13 ; start from this group next round
SA P x37,x38,x39
x43,x44,x45
x32,x33,x34

SD IC x12,x46,x37,x38,x39 
incoresapdoc
GOTO LB92
ENDIF

LB7 ; next group x13

; group alignment not found for this particle, so ignore it 
SD IC x12,x46,x43,x44,x45
incoresapdoc
LB92 ; found group so skip to here

RT SQ
<pcltmpl>x46  ; particle x46
_1
x37
x38,x39

MU
_1
<mask>
_2
*

SH
_2  ; input
<outdir>/rtpma@{******x46} ; output
x58,x59

LB6 ; next particle x12
UD ICE
<outdir>/alnavgsort
DE
<outdir>/alnavgsort
UD ICE
<outdir>/tempsapdoc
DE
<outdir>/tempsapdoc

SD IC COPY
incoresapdoc
<outdir>/saprtpdoc
SD IC END
incoresapdoc

ENDIF ; APSR or SAP
;--- end APSR/SAP module ---
VM
echo Masked {%F7.1%x30} of {%F7.1%x22} particles in {%F7.1%x23} classes
RE
