;makes a model 3D structure of a soccer player
; 8 conformations (left arm up/down, right arm up/down, ball right/left

FR
?IN.Existing folder for soccer dudes?<dir>

; ~~~~~ start ~~~~~

MO 3
_1 ;head
(100,100,100)
SP
N
(1.0)
(9,5) ;radius
(51,15,51) ;origin
(0,0,0) ;end

MO 3
_2 ;body/lowerlegs
(100,100,100)
C ;cylinders
(0.0) ;background
Y ;axis -neck
(5,20) ;radius,length
(51,25) ;x,y
(51,1.0) ;z,density
X ;axis -shoulders
(7,30) ;radius,length
(51,32) ;x,y
(51,1.0) ;z,density
Z ;axis - upperbody
(11,12) ;radius,length
(51,35) ;x,y
(51,1.0) ;z,density
Z ;axis - lowerbody
(10,10) ;radius,length
(51,50) ;x,y
(51,1.0) ;z,density
Y ;axis - L lowerleg
(4,22) ;radius,length
(34,78) ;x,y
(51,1.0) ;z,density
Y ;axis - R lowerleg
(4,22) ;radius,length
(69,78) ;x,y
(51,1.0) ;z,density
Q ;quit

MO 3
_4 ;upperlegs
(100,100,100)
C ;cylinders
(0.0) ;background
X ;axis - L upperleg
(5,20) ;radius,length
(38,51) ;x,y
(51,1.0) ;z,density
Y ;axis - R upperleg
(5,20) ;radius,length
(51,66) ;x,y
(51,1.0) ;z,density
Q ;quit

RT 3D
_4
_3
(45,0,0) ;phi,tta,psi (zyz)

DE
_4

MO 3
_4 ;arms
(100,100,100)
C ;cylinders
(0.0) ;background
X ;axis - arm
(4,22) ;radius,length
(51,51) ;x,y
(51,1.0) ;z,density
Q ;quit

x20=0 ;index for raw projection
x21=0 ;index for noise projection
DO LB1 x11=1,2 ;right arm
  IF(x11.eq.1)THEN
    VM
    echo Right arm is down.
    x31=45
    x32=-22
    x33=-15
  ELSE
    VM
    echo Right arm is up.
    x31=-45
    x32=-22
    x33=-27
  ENDIF
  RT 3D
  _4
  _9 ;arm
  (x31,0,0) ;phi,tta,psi (zyz)

  SH
  _9
  _5 ;L arm
  (x32,x33,0)

  DO LB2 x12=1,2 ;left arm
    IF(x12.eq.1)THEN
      VM
      echo '  'Left arm is up.
      x41=45
      x42=23
      x43=-27
    ELSE
      VM
      echo '  'Left arm is down.
      x41=-45
      x42=23
      x43=-15
    ENDIF
    RT 3D
    _4
    _9 ;arm
    (x41,0,0) ;phi,tta,psi (zyz)

    SH
    _9
    _6 ;R arm
    (x42,x43,0)

    DO LB3 x13=1,2 ;ball
      IF(x13.eq.1)THEN
        VM
        echo '    'Ball on right foot.
        x51=41
      ELSE
        VM
        echo '    'Ball on left foot.
        x51=61
      ENDIF
      MO 3
      _7 ;soccer ball
      (100,100,100)
      SP
      N
      (1.0)
      (8,3) ;radius,inner
      (x51,81,45) ;origin
      (0,0,0) ;end
      
      DE
      _9
      AD
      _1 ;head
      _2 ;body/lowerlegs
      _9
      _3 ;upperlegs
      _5 ;L arm
      _6 ;R arm
      _7 ;ball
      *

      TH
      _9 ;summed volumes
      _8 ;threshold volume
      C
      (0.25,0.0)

      x20=x20+1
      BC
      _8
      <dir>/dude3d{***x20} ;threshold volume
      L ;lowpass
      (4) ;pix
      (1.0) ;weight
      
      PJ 3
      <dir>/dude3d{***x20}
      (100,100)
      _10 ;dude2d@{***x20}
      (0,0,0)
      
      FS
      _10
      FI x27,x28 ;avg,std
      _10
      (9,10)
      
      AR ;adjust to 0+/-1
      _10
      <dir>/dude2d@{***x20}
      ((P1/x28)-x27) ;(pix/std)-avg
      
      ;make 10 copies with added noise
      DO LB4 x14=1,10
        MO ;apply gaussian noise
        _11
        (100,100)
        R
        Y ;N
        (0,2) ;SNR=1
        
        x21=x21+1
        AD
        <dir>/dude2d@{***x20}
        _11
        <dir>/dude2dn@{***x21}
        *
        
        DE
        _11
      LB4

    LB3 ;ball
  LB2 ;left arm
LB1 ;right arm

VM
echo Wrote volumes as dude3d
VM
echo Wrote stack of 2d projections as dude2d
VM
echo Added noise to 2d projections as dude2dn

AS R
<dir>/dude2dn@***
(1-x21)
A
<dir>/dude2dnav
<dir>/dude2dnva

RE
