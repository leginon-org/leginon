; Syntax: @tiltStackSync
; Description:
;   synchronizes two stacks of particles - renumbers particles
;   creates particle list and micro angles docs in preparation for RCT
; Input:
;   untilted stack
;   tilted stack
;   list of corresponding untilted/tilted particles
;   list of euler angles for each particle
;   alignment parameters
; Output:
;   sync'd untilted stack (sequential renumbered particles)
;   sync'd tilted stack (sequential renumbered particles)
;   list of particles in sync'd stacks and tilted micro number
;   angles docs for particles from each micro (numbered according to tilted micro)
;   alignment parameters for sequential particles

FR
?IN.Untilted stack prefix (dir/ustack@)?<ustack>
FR
?IN.Tilted stack prefix (dir/tstack@)?<tstack>
FR
?IN.List that corresponds untilted with tilted stacks (dir/synclist)?<synclist>
FR
?IN.List with micro angles for tilted particles (dir/angles)?<angles>
FR
?IN.Alignment parameters doc (dir/apshdoc)?<alndoc>

FR
?OUT.Sync'd untilted stack prefix (dir/sustack@)?<usync>
FR
?OUT.Sync'd tilted stack prefix (dir/ststack@)?<tsync>
FR
?OUT.Particle list with tilted micro number (dir/tiltlist)?<tiltlist>
FR
?OUT.Template for micro angle docs (dir/dcb****)?<dcbtmpl>
FR
?OUT.Sync'd alignment parameters doc (dir/apshsync)?<alnsync>

; ~~~~~ start ~~~~~

UD N x20
<synclist>

UD N x21
<angles>

IF(x20.ne.x21)THEN
  VM
  echo Particle list and angles list have different numbers of particles
  EN
ENDIF

SD IC NEW
tiltlist_ic
(6,x21)

UD IC x20,x35,x36
<synclist>
FS
<ustack>{******x35}
FI x37,x38
<ustack>{******x35}
(12,2)

MS
_1@
(x37,x38,1)
(x20)

FS
<tstack>{******x36}
FI x37,x38
<tstack>{******x36}
(12,2)

MS
_2@
(x37,x38,1)
(x20)

UD N x27,x28
<alndoc>
IF(x28.ne.15)THEN
  VM
  echo ERROR..This only works for apsh-alignment docs
  EN
ENDIF
IF(x27.lt.x21)THEN
  VM
  echo ERROR..You aligned fewer particles than are in syncd stacks
  EN
ENDIF

DOC SORT
<alndoc>
<alnsync>
(5)
Y

SD IC NEW
alnsync_ic
(x28,x21)

VM
echo Synchronizing tilted and untilted stacks.

x40=0 ;micronumber
x41=0 ;zero
x43=1 ;initialize alignparam search
x32=0 ;initial psi
x33=0 ;initial tta
x34=0 ;initial phi
DO LB1 x11=1,x21 ;for each particle
  ;     key,psi,tta,phi ;tiltaxis,tilt,-untiltaxis
  UD IC x11,x22,x23,x24
  <angles>
  x24=-x24 ;untiltaxis=-phi
  x30=0
  IF(x22.ne.x32)x30=1 ;newpsi
  IF(x23.ne.x33)x30=1 ;newtta
  IF(x24.ne.x34)x30=1 ;newphi
  IF(x30.eq.1)THEN ;found next tilted micro
    IF(x40.gt.0)THEN ;close dcb
      SD E
      <dcbtmpl>x40
    ENDIF
    x40=x40+1 ;increment micnum
    VM
    echo "  Micro"{****x40}
    x42=0 ;number of particles from this micro
    SD / PARAMETERS
    <dcbtmpl>x40
    SD 1,x41,x41,x41,x41,x41,x41
    <dcbtmpl>x40
    SD / FITTED FLAG
    <dcbtmpl>x40
    SD 2,x41,x41,x41,x41,x41,x41
    <dcbtmpl>x40
    SD / (X0,Y0) FOR LEFT IMAGE, (X0,Y0) FOR RIGHT IMAGE, REDUCTION FACTOR
    <dcbtmpl>x40
    SD 3,x41,x41,x41,x41,x41,x41
    <dcbtmpl>x40
    SD / TILT ANGLE (THETA), UNTILTED IMAGE AXIS, TILTED IMAGE AXIS
    <dcbtmpl>x40
    ;    tta,uax,tax
    SD 4,x23,x24,x22,x41,x41,x41
    <dcbtmpl>x40
    x32=x22 ;store psi = tax
    x33=x23 ;store tta = tta
    x34=x24 ;store phi = uax
  ENDIF
  x42=x42+1 ;particle num from micro
  ;     key,pcl,mic, x , y ,pnum,cc
  SD IC x11,x11,x40,x41,x41,x42,x41
  tiltlist_ic
  
  UD IC x11,x25,x26
  <synclist>
  CP
  <ustack>{******x25}
  _1@{******x11}
  
  CP
  <tstack>{******x26}
  _2@{******x11}
  
  DO LB2 x12=x43,x27 ;foreach alignment parameter
    ;         phi,tht,psi,ref,pcl,rt ,xsh,ysh,npj,dif,ccc,rt ,xsh,ysh,mr
    UD IC x12,x51,x52,x53,x54,x55,x56,x57,x58,x59,x60,x61,x62,x63,x64,x65
    <alnsync>
    
    IF(x55.eq.x25)GOTO LB90
  LB2
  
  VM
  echo ERROR..alignment parameter for particle {******x25} was not found
  EN
  
  LB90
  x43=x12 ;start searching from x12 next time
  ;         phi,tht,psi,ref,pcl,rt ,xsh,ysh,npj,dif,ccc,rt ,xsh,ysh,mr
  SD IC x11,x51,x52,x53,x54,x11,x56,x57,x58,x59,x60,x61,x62,x63,x64,x65
  alnsync_ic
  
LB1 ;next particle
UD ICE
<angles>
UD ICE
<alnsync>

SD IC COPY
tiltlist_ic
<tiltlist>
SD IC END
tiltlist_ic

SD IC COPY
alnsync_ic
<alnsync>
SD IC END
alnsync_ic

SD E
<dcbtmpl>x40

CP
_1@
<usync>

CP
_2@
<tsync>

DE
_1

DE
_2

VM
echo wrote euler angles from tiltStackAlign into dcb docs and sertlist.

RE
