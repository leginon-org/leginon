; avgmrg
; combine doc is list of averages to merge generated using web-categorize
;   first column is average number - second column is category

FR
?IN.List of averages (dir/list)?<list>
FR
?IN.Averages template (dir/avg***)?<avgtmpl>
FR
?IN.Combine doc (dir/combinedoc)?<combine>
FR
?OUT.New list of averages (dir/newlist)?<outdoc>
FR
?OUT.New averages template (dir/newavg***)?<outtmpl>

; ~~~~~ start ~~~~~

; --- make list of averages not combined
DOC SUBTRACT
<list>
<combine>
<outdoc>_tmp
(1)
DOC REN
<outdoc>_tmp
<outdoc>_ren
DE
<outdoc>_tmp

UD N x25 ; start numbering merged classes from x25+1
<outdoc>_ren
UD E

SD /       NEWNUM      OLDNUM
<outdoc>
; make new averages
DO LB1 x11=1,x25
UD IC x11,x22
<outdoc>_ren

CP
<avgtmpl>x22
<outtmpl>x11

SD x11,x11,x22 ;newnum,oldnum
<outdoc>

LB1
UD ICE
<outdoc>_ren
DE
<outdoc>_ren

VM
echo -n Retained {%F7.1%x25} averages...

; --- combine averages
DOC SORT ; sort by category
<combine>
<combine>_srt
(2)
Y

UD N x21
<combine>_srt
UD E

x24=0 ; category tracker
x26=0 ; category counter
SD /       NEWNUM       CATEGORY
<outdoc>
DO LB2 x12=1,x21
  UD IC x12,x22,x23 ; avnum,catnum
  <combine>_srt

  IF(x24.lt.x23)THEN ; new category
    IF(x12.ne.1)THEN ; close out earlier category
      SD x25,x25,x24 ; newnum,category
      <outdoc>

      AS R
      <avgtmpl>
      <combine>_{***x25}
      A
      <outtmpl>x25
      _1
      DE
      _1
      DE
      <combine>_{***x25}
    ENDIF

    x20=0
    x24=x23   ; set current category
    x25=x25+1 ; new merged category
    x26=x26+1 ; category counter
  ENDIF

  x20=x20+1
  SD x20,x22,x24
  <combine>_{***x25}
  SD E

LB2
UD ICE
<combine>_srt
DE
<combine>_srt

; save last average
SD x25,x25,x24 ; newnum,category
<outdoc>

AS R
<avgtmpl>
<combine>_{***x25}
A
<outtmpl>x25
_1
DE
_1
DE
<combine>_{***x25}

VM
echo -n Combined {%F7.1%x21} into {%F7.1%x26} averages...

UD N x27
<outdoc>
UD E

VM
echo for a total of {%F7.1%x27} new averages.

EN D
