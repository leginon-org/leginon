[x20,x21] ;radius,tiltangle
; fsc
;  radius is used to make spherical mask
;  tiltangle is angular coverage (if none then =90, rct usually =55)
;  INPUT:
;    list, particle template, apsh doc with angles and shift
;  OUTPUT:
;    dres - you must rename it to avoid overwriting in a loop

FR
?IN.List of particles to split (dir/list)?[list]
FR
?IN.Particles template to project into two volumes (dir/ser@*****)?[pcltmpl]
FR
?IN.APSH format angles doc (dir/apshangles)?[angles]
FR
?OUT.Directory for FSC file, dres (dir)?[dir]

; ~~~~~ start ~~~~~

DOC SORT
[list]
[dir]/list_tmp
(1)
Y

UD N x25
[dir]/list_tmp
UD IC x25,x45
[dir]/list_tmp

FS
[pcltmpl]x45
FI x31,x32
[pcltmpl]x45
(12,2)

MS I
_6@
x31,x32,(1)
x25 ;numpcls
x45 ;maxnum

x30=1
DO LB1 x11=1,x25 ;for each particle
  UD IC x11,x35
  [dir]/list_tmp

  ;     pcl,psi,tta,phi,ref,pcl,crt,cxs,cys,npj,ang,ccc,rt ,xsh,ysh,mr
  UD IC x35,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
  [angles]
  IF(x35.ne.x45)THEN
    VM
    echo ERROR...keys and particle number do not correspond in [angles]
  ENDIF
  x90=0
  IF(x41.ne.0)x90=x90+1
  IF(x46.ne.0)x90=x90+1
  IF(x90.eq.2)THEN
    VM
    echo ERROR...in plane rotation specified twice for particle {%F9.1%x35}
  ENDIF
  RT SQ
  [pcltmpl]x45
  _6@{******x45}
  x46
  x47,x48
LB1 ;next particle
UD ICE
[dir]/list_tmp
DE
[dir]/list_tmp
UD ICE
[angles]

;make volodd _1 and voleven _2
DOC SPLIT
[list]
[dir]/olist_tmp
[dir]/elist_tmp

BP 3F
_6@******
[dir]/olist_tmp
[angles]
*
_1

CP
_1
[dir]/vol_o

DE
[dir]/olist_tmp

BP 3F
_6@******
[dir]/elist_tmp
[angles]
*
_2

CP
_1
[dir]/vol_e

DE
[dir]/elist_tmp

;make mask _3
FS
_1
FI x22,x23,x24
_1
(12,2,1)
IF(x22.ne.x23)THEN
  VM
  echo ERROR...volume is not a box.
  EN
ENDIF
IF(x22.ne.x24)THEN
  VM
  echo ERROR...volume is not a box.
  EN
ENDIF

x90=0
x91=x22/2

MO 3
_3
x22,x22,x22
SP ;sphere
N  ;no doc
(1) ;density
x20,x90 ;outer,inner
x91,x91,x91 ;center
(0,0,0) ;end input

;multiply vols by mask
MU
_1
_3
_4
*

MU
_2
_3
_5
*

RF 3
_4    ;odd volume
_5    ;even volume
(1)   ;ringwidth (1/pixels) (0.5?)
(0.8,1.2) ;scale range
C     ;missing cone
x21   ;tilt angle
(3)   ;3-sigma (factor for noise comparison)
[dir]/dres_tmp
DOC REN
[dir]/dres_tmp
[dir]/dres
DE
[dir]/dres_tmp

DE
_1
DE
_2
DE
_3
DE
_4
DE
_5

RE
