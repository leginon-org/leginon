;rctangles.spi (modified from creatang.pnv)
;for each class writes phi,theta,gamma with key=pclnum
;
; input:
;    rotational alignment parameter for each particle
;    list of all tilted particles (key=pclnum, 2nd register=micronum)
;    documents with angular information (dcb***) created during particle selection 
;    an additive factor for instance if micro#1222 has dcb222, then enter 1000
; output:
;    angles list for each particle (key=pcl:psi,tta,phi,ref,pcl,cum{rt,x,y},npj,ang,cc,cur{rt,x,y},mr)
;
; can account for TiltPicker with images on left and right swapped
;
; caution on calling this procedure: 
;          do not supply arguments as [alndoc]x23 or [serlist]x23
;          instead supply arguments as [alndoc]{***x23}
;          DOC SORT is unable to write [alndoc]x23sort and 
;                 overwrites/deletes original [alndoc]x23 

FR
?IN.Particle alignment doc (dir/alndoc)?[alndoc]
RR x19
?IN.Alignment doc format (APSH=1,APNQ=2,SAP=4)?
FR
?IN.List of ALL tilt particles and their micros (ser/sertlist)?[serlist]
FR
?IN.Template for micro angles (mics/dcb***)?[dcbtmpl]
RR x20
?IN.Factor subtracted to get dcb# (1000)?
FR
?OUT.Particle angle doc (dir/angles)?[angdoc]

; ~~~~~ start ~~~~~
VM
echo '***WARNING: IMPOSING NEGATIVE TILT ANGLE (POSITIVE THETA)***'

;sort alignment doc by particle number
IF(x19.eq.1)THEN ; apshdoc
  VM
  echo -n Using apsh alignment doc.
  DOC SORT
  [alndoc]
  [alndoc]sort
  (5) ; particle num
  Y
  UD N x21
  [alndoc]sort
  UD x21,x41,x42,x43,x44,x45 ; highest pcl number x45
  [alndoc]sort
  GOTO LB22
ENDIF
IF(x19.eq.2)THEN ; apnqdoc
  VM
  echo -n Using apnq alignment doc' '
  DOC SORT
  [alndoc]
  [alndoc]sort
  (6) ; particle num
  Y
  UD N x21
  [alndoc]sort
  UD x21,x44,x42,x46,x47,x48,x45 ; highest pcl number x45
  [alndoc]sort
  GOTO LB22
ENDIF
IF(x19.eq.4)THEN ; sapdoc
  VM
  echo -n Using sap alignment doc.
  DOC SORT
  [alndoc]
  [alndoc]sort
  (1) ; particle num
  Y
  UD N x21
  [alndoc]sort
  UD x21,x45 ; highest pcl number x45
  [alndoc]sort
  GOTO LB22
ENDIF
VM
echo ERROR...Unrecognized alignment doc format.
EN

LB22 ;found alignment doc
VM
echo to write angles for {%F7.1%x45} particles..

SD IC NEW ;psi,tta,phi,...mr (apsh format
angles_ic
(15),x45

x99=0 ; mic counter
DO LB1 x11=1,x21 ; for each aligned particle
;find particle alignment
IF(x19.eq.1)THEN ; APSH
  ;         phi,tht,psi,ref,pcl,rt ,xsh,ysh
  UD IC x11,x41,x42,x43,x44,x45,x46,x47,x48
  [alndoc]sort
  GOTO LB23
ENDIF
IF(x19.eq.2)THEN ; APNQ
  ;         ref,cc, rt ,xsh,ysh,pcl
  UD IC x11,x44,x42,x46,x47,x48,x45
  [alndoc]sort
  GOTO LB23
ENDIF
IF(x19.eq.4)THEN ; SAP
  ;         pcl, rt,xsh,ysh
  UD IC x11,x45,x46,x47,x48
  [alndoc]sort
  GOTO LB23
ENDIF
VM
echo ERROR...Alignment format not recognized.
EN

LB23
UD IC x45,x31,x32 ; pclnum,micnum
[serlist]
IF(x45.ne.x31)THEN
  VM
  echo ERROR in [serlist]...Particle and key numbers must be the same to find micro num.
  EN
ENDIF

x30=x32-x20 ; convert mic num to dcb num
IF(x30.ne.x99)THEN ; this is a new mic,get tilt params and do corrections
  VM
  echo -n Micro {%F7.1%x32}.
  x99=x30 ;save current micnum
  DOC REN
  [dcbtmpl]x99
  [angdoc]bren

  UD 4,x16,x17,x18   ;x16:theta, x17:gamma, x18:phi
  [angdoc]bren
  UD E
  DE
  [angdoc]bren
  IF(x16.eq.0)THEN
    VM
    echo .Error..MICRO WAS NOT TILTED
    EN
  ENDIF

  ;for negative tilting micros have positive theta (with untilt on left & tilted on right)
  ;Web writes +theta,-gamma,-phi 
  ;oldTiltAligner writes +theta,-gamma,+phi or -theta,-phi,+gamma
  ;newTiltAligner writes +theta,-gamma,-phi or -theta,+phi,+gamma
  x23=x17*x18  ;if phi*gamma is negative then must have used oldtiltAligner
  IF(x23.lt.0)THEN ;oldTiltAligner
    VM
    echo -n ".Old TiltAligner..phi=-phi."
    x18=-(x18)  ; new phi = -1(old phi)
  ELSE
    VM
    echo -n ".New TiltAligner or WEB."
  ENDIF
  IF(x16.lt.0.0)THEN ; if theta is negative then tilted on left,untilt on right
    x33=x18    ; store old phi
    x18=x17    ; new phi = old gamma
    x17=x33    ; new gamma = old phi
    x16=-(x16) ;theta inverted
    VM
    echo ".gamma and phi swapped..theta inverted."
  ELSE ;theta is positive
    VM
    echo ".your angles are perfect."
  ENDIF
ENDIF ;new micro

;calculating phi (angular rotation of untilted particle) between 0 and 360
x49=-1*(x46+x17)  ;angle = -1(inplane rotation + gamma) 
IF(x49.lt.0.0)x49=x49+360 ; add 360 to angles less than 0
IF(x49.ge.360.0)x49=x49-360 ; subtract 360 from angles greater than

;save euler angles for particle number x45
; psi (x18) = tilted micrograph rotation from y-axis (called phi in dcb) 
; tta (x16) = tilt angle
; phi (x49) = untilted in-plane rotation + untilted micrograph rotation (gamma)
x50=0
;     pcl psi tta phi,ref,pcl,crt,cxs,cyx,npj,ang,ccc,rt ,xsh,ysh,mr
SD IC x45,x18,x16,x49,x50,x45,x50,x50,x50,x50,x50,x50,x50,x50,x50,x50
angles_ic

LB1  ; next class
UD ICE
[alndoc]sort
DE
[alndoc]sort
SD IC COPY
angles_ic
[angdoc]
SD IC END
angles_ic

VM
echo [angdoc] written.

RE
