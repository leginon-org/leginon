[x23,x21,x22,x20] ; firstcxkey,lastcxkey,imagesize,scale
; cxdots connect the dots with triangles from coords and connection docs
; variables passed:
;	x23=first connection
;	x21=last connection
;	x22=size of output image
;	x20=scale of coordinates
; cxdoc lists the three points to connect in first three columns (makes a triange)
; cordsdoc lists coordinates of points to be connected 

FR
?IN.CONNECTIONS DOC (dir/cxsdoc)?<cxs>
FR
?IN.COORDINATES DOC (dir/crddoc)?<pts>
FR
?IN.OUTPUT IMAGE (dir/file)?<img>

; ~~~~~ start ~~~~~

UD N x24,x25 ;keys,cols
<cxs>

IF(x23.gt.x24)THEN
  VM
  echo Error..first connection key {%F7.1%x23} does not exist.
  EN
ENDIF
IF(x21.gt.x24)THEN
  VM
  echo Error..last connection key {%F7.1%x21} does not exist.
  EN
ENDIF
IF(x25.lt.3)THEN
  VM
  echo Error..connections doc must have 3 coordinates to connect.
  EN
ENDIF

; CREATE NEW BLANK IMAGE
BL
_1 ;<img>
x22,x22,(1)
N
(0.0)

DO LB1 x11=x23,x21  ; for each connection

;READ CONNECTIONS
UD IC,x11,x31,x32,x33
<cxs>

;READ POINTS TO CONNECT
UD IC,x31,x41,x42
<pts>

UD IC,x32,x51,x52
<pts>

UD IC,x33,x61,x62
<pts>

x41=x41/x20
x42=x42/x20
x51=x51/x20
x52=x52/x20
x61=x61/x20
x62=x62/x20

; CONNECT THE DOTS

PT
_1 ;<img> ;  (x22,x22) ; NOT SPECIFIED BECUASE BLANK IMAGE GIVEN EARLIER
T ; TRIANGLE
x41,x42
x51,x52
x61,x62
N

LB1  ; next triangle x11

UD ICE
<cxs>

UD ICE
<pts>

CP
_1
<img>

DE
_1

VM
echo Made shape...<img>

RE
