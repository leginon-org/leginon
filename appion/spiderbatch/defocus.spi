; DEFOCUS.SPI estimates defocus from 2D power spectra.
;              Uses Spider operation TF ED
; Output:
;    defocus : doc file of defocus estimates
;    ctf***  : (deleted) doc files (1 for each input) with spectrum, envelope, noise 

FR
?IN.List of micros (dir/file)?[miclist]
FR
?IN.Power spectra prefix (dir/pow assumes ****)?[pow]
RR x55
?IN.Pixel Size (A/Pix)?
RR x56
?IN.Spherical aberration (Cs)?
RR x57
?IN.Amplitude contrast ratio?
RR x58
?IN.Lambda?
FR
?OUT.Existing output folder (dir)?<outdir>

; ~~~~~ start ~~~~~

UD N x40
[miclist]

VM
echo -n Calculating defocuses from {%F7.1%x40} power spectra..

SD /       micro     defocus     def.corr     astig.ang    astig.mag    cutoff.freq
[outdir]/defocus

DO LB1 x11=1,x40
UD IC x11,x41
[miclist]

   TF ED x12,x13,x14,x15,x16 ;ang,mag,astdef,overdef,cutoff
   [pow]{****x41}      ; input 2D spectrum
   (x55,x56)           ; pixel size (A), spherical aberration (mm)
   (x58)               ; lambda
   (x57)               ; ampl. contrast ratio
   [outdir]/ctftmp     ; output doc file

   DE
   [outdir]/ctftmp

   SD x11,x41,x15,x14,x12,x13,x16
   [outdir]/defocus

LB1 ;next micro
UD ICE
[miclist]
SD E
[outdir]/defocus

VM
echo .done.

RE
