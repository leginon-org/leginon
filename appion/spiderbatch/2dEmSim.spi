; 2demsim
; Description: simulates ctf on a square image
; Procedure:
;   1. calculates ctf to simulate em (ft-tfc-mu-ft)
;   2. adds noise (mo-ad)
; Input:
; - image
; - negative stain (-1) or cryo (1)
; - snr passed as variable (usually around 5 is OK)
; - parameters_doc contains the following in registers 
;     1. cs
;     2. defocus(A)
;     3. lambda(A)
;     4. maxfreq(1/pix)
;     5. source(1/A)
;     6. defocus spread(A)
;     7. amplitude contrast ratio
;     8. gaussian envelope halfwidth
; Output:
; - ctf and noisy image

FR
?IN.Input image (dir/img)?<img>

RR x40
?IN.Cryo=(1) or Stain=(-1)?

RR x41
?IN.Signal to noise ratio (5)?

RR x42
?IN.Pixel size (3.0 A/pix)?

RR x43
?IN.Acceleration voltage (120 kV)?

RR x44
?IN.Defocus (under is positive, 1000 nm)?

RR x45
?IN.Astigmatism (400 Angstroms)?

RR x46
?IN.Astigmatism angle (40 degrees)?

RR x47
?IN.Amplitude contrast ratio (0-1, usually ~0.1)?

RR x49
?IN.Gaussian envelope halfwidth (0.05-10000 1/A)?

FR
?OUT.Output image (dir/img)?<outimg>

; ~~~~~ start ~~~~~

RR x48
(2.0)   ;?IN.Spherical aberration constant (2.0 mm)?
RR x50
(1000)  ;?IN.Defocus spread (1000 A)?
RR x51
(0.005) ;?IN.Source size (0.005 1/A)?

x52=1/(2*x42) ;maximum spatial frequency (1/A)
;convert kV to lambda from table
IF(x43.eq.100)x53=0.03701
IF(x43.eq.120)x53=0.03349
IF(x43.eq.140)x53=0.03074
IF(x43.eq.160)x53=0.02851
IF(x43.eq.180)x53=0.02665
IF(x43.eq.200)x53=0.02508
IF(x43.eq.300)x53=0.01969
IF(x43.eq.400)x53=0.01644

x44=x44*10 ;defocus: convert nm to A

FS
<img>
FI x54,x55
<img>
(12,2) ;x,y

IF(x54.ne.x55)THEN
  VM
  echo 'ERROR: dimensions are '{%F9.1%x54} by {%F7.1%x55}
  VM
  echo 'EM simulation only works on square images'
  EN
ENDIF

;---piece together 1000 pixel fragments---
x55=int(x54/1000)+1 ;# fragments

BL
_4
(x54,x54)
N
(0)

DO LB1 x11=1,x55 ;for each column
  x58=(x11-1)*1000+1 ;topleft of column
  x56=x58-12 ;topleft windowed

  DO LB2 x12=1,x55 ;for each row
    x59=(x12-1)*1000+1 ;topleft of row
    x57=x59-12 ;topleft windowed

    WI
    <img>
    _5
    (1024,1024)
    (x56,x57)

    FT    ;FOURIER THANSFORM THE ORIGINAL IMAGE (PROJECTION OF 11.5A RIBOSOME VOLUME)
    _5     ;INPUT ORIGINAL IMAGE
    _1     ;OUTPUT FT OF ORIGINAL IMAGE

    TF C  ;GENERATE COMPLEX CTF (i.e. real part= CTF; imaginary part=0)
    _2          ;OUTPUT COMPLEX CTF
    (x48)       ;CS[MM]
    (x44,x53)   ;DEFOCUS(A), LAMBDA(A)
    (1024)      ;DIMENSION OF THE 2D ARRAY
    (x52)       ;MAXIMUM SPATIAL FREQUENCY
    (x51,x50)   ;SOURCE SIZE[A-1], DEFOCUS SPREAD[A]
    (x45,x46)   ;ASTIGMATISM[A], AZIMUTH[DEG]
    (x47,x49)   ;AMPLITUDE CONTRAST RATIO, GAUSSIAN ENVELOPE HALFWIDTH
    (x40)       ;+1=CRYO OR -1=NEGATIVE STAIN

    MU    ;MULTIPIY FT OF ORIGINAL IMAGE WITH CTF
    _1        ;FT OF ORIGINAL IMAGE
    _2        ;COMPLEX CTF FROM ABOVE
    _3        ;RESULT OF MULTIPLICATION
    *

    FT    ;FOURIER(INVERSE) PRODUCT
    _3       ;INPUT FOURIER FILE
    _2       ;OUTPUT REAL SPACE VERSION OF CONVOLUTED IMAGE(="EM IMAGE")

    WI   ;REMOVE BORDER OF IMAGE
    _2           ;REAL SPACE CTFd IMAGE
    _6           ;CROPPED WINDOW
    (1000,1000)  ;DIMENSIONS
    (13,13)      ;TOPLEFT

    IN
    _6   ;CROPPED WINDOWN
    _4   ;INSERTED INTO LARGER IMAGE
    (x58,x59)
  LB2
LB1

DE
_1
DE
_2
DE
_3
DE
_5
DE
_6

FS
_4
FI x24,x25
_4
(9,10) ;avg,std

x25=x25/x41 ;noise standard deviation
MO      ;CREATE NOISE IMAGE
_7          ;NOISE IMAGE OUTPUT
(x54,x54)   ;DIMENSIONS OF OUTPUT FILE
R           ;RANDOM UNIFORM
y           ;GAUSSIAN DISTRIBUTION
x24,x25     ;MEAN, ST. DEV OF GAUSSIAN DISTRIBUTION 

AD   ;ADD NOISE TO CREATE MORE REALISTIC EM IMAGE
_4        ;NOISE-FREE IMAGE FILE FROM ABOVE
_7        ;NOISE FILE
<outimg>  ;RESULTING FILE
*

;TF CTS      ;CTF CORRECTION
;<outdir>/add*   ;TEMPLATE FOR 2-D IMAGE FILE
;(1)             ;FILE NUMBERS (IN THIS CASE ONLY ONE) 
;_2              ;2-D CTF FILE GENERATED BY 'TF C'
;100             ;SIGNAL-TO-NOISE RATIO
;res1            ;RESTORED IMAGE FILE

;FQ        ;FILTER RESULT TO THE BANDWIDTH OF THE SIGNAL
;<outdir>/res1   ;INPUT FILE
;<outdir>/fil1   ;OUTPUT FILE
;(5)             ;FILTER TYPE = FERMI LOW-PASS
;(0.25)          ;FILTER RADIUS
;(0.05)          ;TEMPERATURE PARAMETER FOR THE FERMI FILTERS 

DE
_4
DE
_7

RE
