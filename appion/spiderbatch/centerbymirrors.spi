(x21,x22) ;outsx,outsy
; Syntax: @centerbymirrors(xsh,ysh)
; Description:
;   determines shift to center an image by comparing with mirrored images
;   assumes that the center of object is already within 33% imagesize of center
; Output:
;   xshift (x21) and yshift (x22) can be directly applied to shift object into center
;   centered image

FR
?IN.Image (dir/file)?<in>
FR
?OUT.Image (dir/file)?<out>

; ~~~~~ start ~~~~~
CP
<in>
_1

; find yshift
MR
_1
_2
X ;interchange rows

CC N
_1
_2
_4

; find xshift
MR
_1
_2
Y ;interchange columns

CC N
_1
_2
_5

; find xshift and yshift
MR
_2
_3
X ;interchange columns

CC N
_1
_3
_6

;only allow shift within radius of 1/3 image
FS
_1
FI x23
_1
(12) ;nsam

x30=x23/3
MO
_2
x23,x23
C ;circle
x30
x30=x23/10
BC
_2
_3
L   ;lowpass
x30,x30   ;filtersize
(1.0)   ;weight
TH
_3
_2
C
(1.0,0.0)

MU
_4
_2
_3
*

MU
_5
_2
_4
*

MU
_6
_2
_5

x30=(x23/2)+1 center of image
PK M x31,x32,x33,x34 ;x34 is -yshift
_3
x30,x30
PK M x41,x42,x43,x44 ;x43 is -xshift
_4
x30,x30
PK M x51,x52,x53,x54 ;x53,x54 is -xshift,-yshift
_5
x30,x30

x21=-(x43+x53)/4
x22=-(x34+x54)/4

SH
_1
<out>
x21,x22

DE
_1
DE
_2
DE
_3
DE
_4
DE
_5
DE
_6

RE
