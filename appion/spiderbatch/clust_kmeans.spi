[x20,x21] ;numfactors,numgrps ;[x22]=weights?from doc?
;clust_kmeans[x20,x21]
;if x20=0 then will ask for discontinuous factors
;output:
; kmlist, km***, kmav@***, kmva@***

FR
?IN.Coran prefix (assumes _IMC)?[prefix]
IF(x20.eq.0)THEN
  FR
  ?IN.Factors to keep ((1,2-10,20))?[factors]
ENDIF
FR
?IN.Aligned particles template (dir/rtp@*****)?[pcltmpl]

; ~~~~~ start ~~~~~

IF(x20.eq.0)THEN ;discontinuous factors
  VM
  echo -n Generating {%F7.1%x21} k-means classes with discontinuous factors..

  ;intergrp,intragrp,coleman,harabasz,daviesbouldin
  CL KM x91,x92,x93,x94,x95
  [prefix]_IMC
  x21       ;numgrps
  [factors] ;factors
  (0)       ;weight
  (1457)    ;just a random number
  [prefix]_km***
  [prefix]_kmlist
ELSE ;continuous factors
  VM
  echo -n Generating {%F7.1%x21} k-means classes with {%F7.1%x20} factors..

  ;intergrp,intragrp,coleman,harabasz,daviesbouldin
  CL KM x91,x92,x93,x94,x95
  [prefix]_IMC
  x21     ;numgrps
  (1-x20) ;factors
  (0)     ;weight
  (1457)  ;just a random number
  [prefix]_km***
  [prefix]_kmlist
ENDIF

DE
[prefix]_kmlist

SD IC NEW
kmlist_ic
(2),x21 ;cols,keys

DO LB1 x11=1,x21 ;for each class
  UD N x42  ; how many images in this class
  [prefix]_km{***x11}
  UD E

  SD IC x11,x11,x42 ; essentially doc key but that command doesn't work on cluster
  kmlist_ic

  AS R
  [pcltmpl]              ; IMAGE TEMPLATE
  [prefix]_km{***x11}    ; LIST OF IMAGES
  A     ; ALL IMAGES
  [prefix]_kmav@{***x11} ; AVERAGE IMAGE
  [prefix]_kmva@{***x11} ; VARIANCE IMAGE
LB1

SD IC COPY
kmlist_ic
[prefix]_kmlist
SD IC END
kmlist_ic

VM
echo .done.

RE
