; Description: Cut particles from from tilted and untilted micrographs
; Input:
;   Tilt pairs are sequentially numbered
;     ex. mic1001.spi and mic1002.spi are tilted and untilted micros pairs
;   Coordinate files contain first three columns: pcl#,x,y
;     dct**** and dcu**** must be numbered according to tilted (first) micrograph 
;     ex. dct1001.spi and dcu1001.spi are the corresponding particle coordinates
;   Boxsize = 50% larger than particle size
; Output: sert@******, seru@******, serulist, sertlist

FR
?IN.Micros List (dir/file)?<miclist>
FR
?IN.Micros Template (dir/pre****)?<mictmpl>
FR
?IN.Tilt Coordinates Template (dir/dct****)?<dct>
FR
?IN.Untilt Coordinates Template (dir/dcu****)?<dcu>
RR x21
?IN.Box Size (#)?
RR x24
?IN.Decimation Factor of Coordinates (#)?
FR
?OUT.Folder To Create For Particles (dir)?<dir>

; ~~~~~ start ~~~~~

UD N x31 ;number of micros
<miclist>

x29=x31/2 ; pairs of micros
x28=(x31/2)-(int(x31/2)) ;even or odd number of micros
IF(x28.ne.0)THEN
  VM
  echo ERROR...You Have {%F7.1%x31} Micros. All Micros Must Have Pairs!
  EN
ENDIF

SD IC NEW
sertlist_ic
(6,999999)
SD IC NEW
serulist_ic
(6,999999)

VM
echo Windowing particles with box size {%F7.1%x21}

x23=int(x21/2)+1    ;coordinate of window center
x23=x23-1 ;value to subtract to get top left coord

VM
mkdir -p <dir>

x20=0.0          ;particle counter
DO LB1 x11=1,x29 ; for each pair of micrographs
  x30=2*x11-1 ;get tilted micro #
  UD IC x30,x32
  <miclist>

  x30=2*x11   ;get untilted micro #
  UD IC x30,x33
  <miclist>

  FS
  <mictmpl>x32
  FI x50,x51
  <mictmpl>x32
  (12,2)

  FS
  <mictmpl>x33
  FI x52,x53
  <mictmpl>x33
  (12,2)

  DOC REN
  <dct>x32
  <dir>/dctr_tmp
  DOC REN
  <dcu>x32
  <dir>/dcur_tmp

  UD N x35  ;number of tilted picks
  <dir>/dctr_tmp
  UD N x36  ;number of untilted picks
  <dir>/dcur_tmp

  VM
  echo ' '
  VM
  echo Tilted Micro {****x32} Has {%F7.1%x35} Particles 
  VM
  echo Untilted Micro {****x33} Has {%F7.1%x36} Particles

  IF(x35.lt.x36)THEN
    VM
    echo '   'ERROR...Tilted micro has fewer picks than untilted pair. 
  ENDIF
  IF(x35.gt.x36)THEN
    VM
    echo '   'ERROR...Tilted micro has more picks than untilted pair.
  ENDIF

  x18=0 ;current tilted key
  x19=0 ;current untilted key
  x25=0 ;skipped particle counter
  x44=x20 ;end of previous count
  x45=x20 ;end of previous count
  x37=x35+x36
  DO LB2 x12=1,x37 ; for each key
    x18=x18+1 ; next tilted key
    x19=x19+1 ; next untilted key

    x97=0
    IF(x18.gt.x35)THEN ;at end of tiltdoc
      x97=x97+1
    ELSE
      UD IC x18,x38,x40,x41 ;pcl#,x,y
      <dir>/dctr_tmp
      x40=x40*x24 ;if x=dx then x needs to be scaled
      x41=x41*x24 ;if y=dy then y needs to be scaled
    ENDIF

    IF(x19.gt.x36)THEN ;at end of untiltdoc
      x97=x97+1
    ELSE
      UD IC x19,x39,x42,x43 ;pcl#,x,y
      <dir>/dcur_tmp
      x42=x42*x24 ;if x=dx then x needs to be scaled
      x43=x43*x24 ;if y=dy then y needs to be scaled
    ENDIF
    IF(x97.eq.2)GOTO LB97 ; everything has been windowed

    x34=0
    ;---tilted particle
    ;top left
    x46=x40-x23
    x47=x41-x23
    ;bottom right
    x48=x40+x23
    x49=x41+x23
    IF(x46.lt.1)x34=1
    IF(x47.lt.1)x34=1
    IF(x48.gt.x50)x34=1
    IF(x49.gt.x51)x34=1
    ;---untilted particle
    ;top left
    x56=x42-x23
    x57=x43-x23
    ;bottom right
    x58=x42+x23
    x59=x43+x23
    IF(x56.lt.1)x34=1
    IF(x57.lt.1)x34=1
    IF(x58.gt.x52)x34=1
    IF(x59.gt.x53)x34=1
    IF(x34.eq.1)THEN
    ;  VM
    ;  echo '   'Particle Beyond Micrograph Edge...skipped.
      x25=x25+1
      GOTO LB99
    ENDIF

    IF(x38.gt.x39)THEN
      VM
      echo '   'Current tilted {%F7.1%x38} is greater than current untilted {%F7.1%x39}
      x18=x18-1 ; repeat x38 until x39 catches up
      GOTO LB98 ; current tilted particle is greater than current untilted - wait
    ENDIF

    x44=x44+1 ; current particle number
    WI  ;---cut tilted particle
    <mictmpl>x32
    <dir>/sert@{******x44}
    x21,x21  ;size
    x46,x47  ;x,y

    ;         pcl,mic, x , y ,pic,cc
    SD IC x44,x44,x32,x40,x41,x18
    sertlist_ic

    LB98 ; catch up untilted particles because tilted are ahead

    IF(x39.gt.x38)THEN
      VM
      echo '   'Current untilted {%F7.1%x39} greater than current untilted {%F7.1%x38}
      x19=x19-1 ; increment key for next x39
      GOTO LB99 ; current untilted particle is greater than current tilted - wait
    ENDIF

    x45=x45+1 ; current particle number
    WI  ;---cut untilted particle
    <mictmpl>x33
    <dir>/seru@{******x45}
    x21,x21  ;size
    x56,x57  ;x,y

    ;         pcl,mic, x , y ,pic,cc
    SD IC x45,x45,x33,x42,x43,x19
    serulist_ic

    LB99 ; catch up tilted particles because untilted are ahead
  LB2 ; next particle x12
  LB97 ; all particle were windowed...escape from loop
  IF(x97.ne.2)THEN ;end of both docs was not reached
    IF(x18.lt.x35)THEN
      VM
      echo ERROR...more tilted keys {%F7.1%x35} than number read {%F7.1%x18}.
      EN
    ENDIF
    IF(x19.lt.x36)THEN
      VM
      echo ERROR...more untilted keys {%F7.1%x36} than number read {%F7.1%x19}.
      EN
    ENDIF
  ENDIF

  IF(x38.ne.x39)THEN
    VM
    echo ERROR, FIX DOCUMENTS! There are not the same number of tilted and untilted particles.
    EN D
  ENDIF
  IF(x44.ne.x45)THEN
    VM
    echo ERROR, FIX DOCUMENTS! There are not the same number of tilted and untilted particles.
  ENDIF
  VM
  echo '   'Skipped {%F7.1%x25} Particles Beyond Micrograph Edge

  x38=x44-x20
  x39=x45-x20
  VM
  echo '   'Windowed {%F7.1%x38} tilted and {%F7.1%x39} untilted particles.

  x48=x20+1   ;first particle from this pair of micros
  x20=x44     ;cumulative number of particles from all micros

  ;save information about untilted particles
  ;x32/x33 is the micrograph number
  ;x48 is the first particle for that micrograph x32 and x33
  ;x20 is the last particle for that micrograph x32 and x33
  ;x38=x39 are the total number of particles in micrograph
  SD x11,x32,x48,x20,x38
  <dir>/particles_t
  SD x11,x33,x48,x20,x39
  <dir>/particles_u

  UD ICE
  <dir>/dctr_tmp
  UD ICE
  <dir>/dcur_tmp
  DE
  <dir>/dctr_tmp
  DE
  <dir>/dcur_tmp
LB1 ; next micro x11
UD ICE
<miclist>

SD IC COPY
sertlist_ic
<dir>/sertlist
SD IC END
sertlist_ic

SD IC COPY
serulist_ic
<dir>/serulist
SD IC END
serulist_ic

VM
echo {%F7.1%x20} total particle pairs were windowed. Now make a nice volume!

RE
