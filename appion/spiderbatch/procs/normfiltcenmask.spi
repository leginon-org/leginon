; preprocess particles
; normalize, filter, center/crop, mask corners
; version 20090123

FR L ; directory containing stack@******
[pcldir]stacks/alignedstacks/tiltSync1
FR L ; stackname in pcldir
[stack]seru
FR L ; list in pcldir (will make if it doesn't exist)
[list]ulist
RR x22 ;lowpass filter (A)
18
RR x23 ;hipass filter (A)
330
RR x24 ;A/pixel
2.17
RR x20 ;new box size (pixels) - 3xParticleRadius
150

; ~~~~~ start ~~~~~
MD
SET MP
(0)

x99=0
IQ FI x99
[pcldir]/[list]
IF(x99.eq.0)THEN ;make a list of particles
  FI x26
  [pcldir]/[stack]@
  (26) ;maxnum

  @listfiles
  [pcldir]/[stack]@******
  [pcldir]/[list]
  (x26)
ENDIF

;normradius halfway between particle and box edge
x21=2.5*x20/6 ;normalization radius

x99=0
IQ FI x99
[pcldir]/n[stack]av
IF(x99.eq.0)THEN
  ;normalize particles
  @pclnorm[x21]
  [pcldir]/[list]
  [pcldir]/[stack]@******
  [pcldir]/n[stack]@******

  AS R
  [pcldir]/n[stack]@******
  [pcldir]/[list]
  A
  [pcldir]/n[stack]av
  [pcldir]/n[stack]va
ENDIF

x99=0
IQ FI x99
[pcldir]/fn[stack]av
  IF(x99.eq.0)THEN
  ;butterworth filter particles
  x22=x22 ;lowpass
  x23=x23 ;highpass
  x24=x24 ;apix
  @filter[x22,x23,x24]
  [pcldir]/[list]
  [pcldir]/n[stack]@******
  [pcldir]/fn[stack]@******

  AS R
  [pcldir]/fn[stack]@******
  [pcldir]/[list]
  A
  [pcldir]/fn[stack]av
  [pcldir]/fn[stack]va
ENDIF

x99=0
IQ FI x99
[pcldir]/cfn[stack]av
IF(x99.eq.0)THEN
  ;center and crop stack
  @centerbymirrors[x31,x32]
  [pcldir]/fn[stack]av
  _10
  DE
  _10

  x30=0 ;rotation
  x31=int(x31) ;x-shift
  x32=int(x32) ;y-shift
  x33=1 ;key
  ;      rt ,xsh,ysh
  SD x33,x30,x31,x32
  [pcldir]/cg_doc
  SD E
  [pcldir]/cg_doc

  FS
  [pcldir]/fn[stack]av
  FI x33
  [pcldir]/fn[stack]av
  (12)

  UD N x34
  [pcldir]/[list]
  UD E
  MS I
  _10@
  x33,x33,(1)
  x34
  x34

  @rtsq_ic
  (5) ;all
  [pcldir]/[list]
  [pcldir]/cg_doc
  [pcldir]/fn[stack]@******
  _10@****** ;[pcldir]/cfn[stack]@******

  x20=x20
  IF(x20.lt.x33)THEN
    x35=int((x33-x20)/2) ;top left
    @crop(x35,x35,x20,x20) ;[topx,topy,xsize,ysize]
    [pcldir]/[list]
    _10@******
    [pcldir]/cfn[stack]@******
  ELSE
    UD N x25
    [pcldir]/[list]
    CP
    _10@
    [pcldir]/cfn[stack]@
    x25
  ENDIF

  DE
  _10

  AS R
  [pcldir]/cfn[stack]@******
  [pcldir]/[list]
  A
  [pcldir]/cfn[stack]av
  [pcldir]/cfn[stack]va
ENDIF

;make mask for corners of particles
FS
[pcldir]/cfn[stack]av
FI x20
[pcldir]/cfn[stack]av
(12)
x27=x20*2     ;make box 2x larger for filtering mask
x28=x20+0.5   ;center of box
x29=x20*3/5   ;maskradius - just cuts corners (if box=100;radius is 30)
PT
_11
x27,x27
C
x28,x28
x29
N

x29=x20/10 ;filterwidth
BC
_11
_12
L   ;lowpass
x29,x29
(1) ;weight

x29=x20/2 ;topleft
WI
_12
_13
(x20,x20)
x29,x29

FS
_13
FI x27,x28
_13
(7,8) ;max,min
x29=(1/(x27-x28)) ;slope
x30=0-(1/(x27-x28))*x28 ;intercept
AR ;rescale after filter
_13
[pcldir]/fcirclemask
P1*x29+x30
DE
_11
DE
_12
DE
_13

@maskshift
[pcldir]/[list]
[pcldir]/cfn[stack]@******
[pcldir]/fcirclemask
[pcldir]/mcfn[stack]@******
[pcldir]/mcfn_shiftdoc

AS R
[pcldir]/mcfn[stack]@******
[pcldir]/[list]
A
[pcldir]/mcfn[stack]av
[pcldir]/mcfn[stack]va

EN D
