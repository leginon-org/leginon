; 20080428
; classifyauto + merge + fraln10x each class
FR L      ; list of classes
<clalist>cl3/grpdoc
FR L      ; class averages
<clavgs>cl3/apsrgrp/alni@*****
FR L      ; decimation used for classification
<decimate>4
FR L      ; mask for classification
<mask>circle28
FR L      ; number of classes to generate
<numgrp>20
FR L      ; number of factors
<numfact>25
FR L      ; scale of factor weights
<weight>0.1
FR L      ; output directory
<outdir>cl3/apsrcl2
FR L      ; classification prefix
<prefix>c28
FR L      ; class particle lists
<cltmpl>cl3/avggrp/thr*****
FR L      ; particle template
<pcltmpl>ser2/mfseru@******
FR L      ; radius for alignment
<radius>27
FR L      ; number of apsrrounds
<apsrtest>5
FR L      ; reference to orient apsr averages
<reftmpl>ksq_ipshmrav

; --- let the fun begin ---
MD
VB OFF
MD
SET MP
(0)

x98=<decimate>
x99=<radius>
x94=<apsrtest>

x95=<numfact>
x96=<weight>
x97=<numgrp>

VM
mkdir <outdir>

@dcs[x98]
<clavgs>
<clalist>
<outdir>/tmpdavg@******

DC S
<mask>
_11
x98,x98

@classifyauto[x95,x96,x97]
<outdir>/tmpdavg@******
<clavgs>
<clalist>
_11
<outdir>/<prefix>

DE
<outdir>/tmpdavg
DE
_11

@mergesubclasslists
<outdir>/<prefix>
<cltmpl>

MD
SET MP
(1)
UD N x21
<outdir>/<prefix>_mrglist
DO LB1 x11=1,x21 ; for each group
  UD IC x11,x22
  <outdir>/<prefix>_mrglist

  @fraln[x99,x94]
  <outdir>/<prefix>_mrg{***x22} 
  <pcltmpl>
  *
  <outdir>/apsr{***x22} ;apsrdir

  @alnapsr[x99]
  <outdir>/apsr{***x22} ;apsrdir
  <outdir>/<prefix>_mrg{***x22}  ;list of aligned particles
  (3)                   ;refalign to oriented average
  <reftmpl> ;x22        ;reference image

  AS R
  <outdir>/apsr{***x22}/lasti**
  (1-10)
  A
  <outdir>/lastiav{***x22}
  <outdir>/lastiva{***x22}
  DE
  <outdir>/lastiva{***x22}
LB1 ; next group
UD ICE
<outdir>/<prefix>_mrglist

EN D
; ---use apsrstat to evaluate class averages---
MD
SET MP
(0)

UD N x21
<outdir>/<prefix>_mrglist

SD /    Class     RT-ERR     SH-ERR
<outdir>/apsrerr

VM
echo Combining 10xAPSR for each of {%F7.1%x21} classes.
DO LB3 x11=1,x21  ;for each group

UD x11,x22
<outdir>/<prefix>_mrglist
UD E

VM
echo ---- Class APSR {***x22} ----

@apsrstat
<outdir>/apsr{***x22}
<outdir>/apsr{***x22}/apsrsum
<outdir>/apsrerr{***x22}

DE
<outdir>/apsr{***x22}/apsrsum

x25=0 ; rotation error
x26=0 ; shift error
DO LB2 x12=1,10
  UD IC x12,x10,x23,x24
  <outdir>/apsrerr{***x22}

  x25=x25+x23
  x26=x26+x24
LB2
UD ICE
<outdir>/apsrerr{***x22}

x25=x25/10
x26=x26/10

SD x11,x11,x25,x26
<outdir>/apsrerr

LB3
SD E
<outdir>/apsrerr

EN D

