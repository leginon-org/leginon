(x15) ;radius(output)
; Syntax: @findradius(radius)
; Description:
;  determines radius of an image
;  warning: radius will be overwritten with returned value
FR
?IN.Image (dir/img)?<img>

; ~~~~~ start ~~~~~

VM
echo -n Finding radius of <img>..

x90=0
IQ FI x90
<img>
IF(x90.eq.0)THEN
  VM
  echo ERROR..<img> does not exist.
  EN
ENDIF

; because of tendency to find radius too large, threshold the lowest densities
FS
<img>
;  max,min,avg,std
FI x23,x24,x25,x26
<img>
(7,8,9,10)

x28=x25+0.5*x26 ;above mean
IF(x28.ge.x23)x28=(x25+x23)/2 ;halfway between max and avg

;x27=x25 ;avg
x27=x25-0.5*x26 ;below mean
IF(x27.le.x24)x27=(x25+x24)/2 ;halfway between min and avg

TH
<img>
_2
c
x28,x27 ;upper,lower

;CP
;<img>
;_2

; rotationally average
RO
_2 ;[dir]/apsrav{***x31}
_1
DE
_2

FS
_1
;  col,max,min,avg,std
FI x22,x23,x24,x25,x26
_1
(12,7,8,9,10) ;ncol,max,min,avg,std

;radius threshold must be below x27
x27=x25-0.5*x26 ;below mean
IF(x27.le.x24)THEN
  VM
  echo -n 'Thresh=Mean(Avg+Min+Max)'..
  x27=(x23+x24+x25)/3 ;midpoint of avg, max, and min
ENDIF

; upper threshold
x28=x25+0.5*x26
x29=(x25+x23)/2
IF(x28.gt.x29)x28=x29 ;x28 is the lesser of x29 and x28

x36=0.0 ;radius above upper threshold
x30=x22-1 ;lastradius to check
;--- find first radius below lower threshold after crossing upper
DO LB1 x11=2,x30 ;for each pixel

x13=x11-1
x14=x11
x15=x11+1
GP x32
_1
x13,(1)
GP x33
_1
x14,(1)
GP x34
_1
x15,(1)

x35=(x32+x33+x34)/3 ; current window
IF(x35.gt.x28)x36=x14 ;radius above upper threshold

IF(x36.eq.0.0)GOTO LB21 ;hasn't crossed upper threshold
IF(x35.gt.x27)GOTO LB21 ;smoothed value is above lower threshold

IF(x32.lt.x33)THEN
  x33=x32 ;x33 becomes min of x32 and x33
  x14=x13 ;x14 becomes min of x13 and x14
ENDIF
IF(x33.lt.x34)THEN
  x34=x33 ;x34 becomes min of x33 and x34
  x15=x14 ;x15 becomes min of x14 and x15
ENDIF

GOTO LB22 ;escape because criteria satisfied

LB21 ;try again
LB1  ;for each pixel
LB22 ;escape x34 is min value, and x15 is min radius
DE
_1

IF(x11.eq.x30)x15=0 ;min at last radius - 0=failed to find decent radius
VM
echo ' '{%F7.1%x15}' pixels'

RE
