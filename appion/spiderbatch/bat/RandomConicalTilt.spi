; Procedure: Backprojection of RCT volumes for each class
; version 20090722

FR L     ;list of all tilted particles (contains micronum in 2nd column)
<sertlist>pcl/sertlist

FR L     ;tilted particles template for backprojecting volume (can be filtered/masked)
<pcltmpl>pcl/mfnsert@******

FR L     ;unfiltered tilted particles template for calculating resolution
<initpcl>pcl/nsert@******

FR L     ;pixSize of tilted particles (in Angstroms/pixel)
[Apix]4.2

FR L     ;tiltangle of goniometer during data collection
[tiltangle]-60

FR L     ;tempate for docs with micrograph tilt parameters (dcb docs)
<dcbtmpl>mics/dcb****

FR L     ;alignment params of untilted particles
<alndoc>ital1/apshdoc

FR L     ;tiltdirection - if this is wrong your volumes will be mirrored!
[tiltindex]1 ;far from focus (1=left,2=right,3=top,4=bottom)

FR L     ;list of classes
<grplist>ital1/r20/grp_count

FR L     ;classlists template
<grptmpl>ital1/r20/grp***

FR L     ;output directory for for volumes and euler params (voea style)
<dir>ital1/bp1

; ~~~~~~~~~~ start ~~~~~~~~~
MD
set mp
(0)

MD
vb off

VM
mkdir -p <dir>

x41=[Apix]
x42=[tiltindex]
x43=abs([tiltangle])

;--- combine alignment param and tilt angle ---
@rctanglesmr
<alndoc>
<sertlist>
<dcbtmpl>
(0)       ;factor to add to dcb to get image number
(x42)
<dir>/rctangles

UD N x29
<sertlist>
UD x29,x30
<sertlist>
UD E

;--- automatically find radius ---
FS
<pcltmpl>x30
FI x20
<pcltmpl>x30
(12)
x23=int(x20*3/7)
VM
echo Backprojection radius set to {%F7.1%x23} pixels.

;--- generate and translational refine volume for each class ---
x31=30 ;first iteration volume filter
x32=20 ;last iteration volume filter

UD N x21
<grplist>
DO LB1 x11=1,x21 ;for each class
  UD IC x11,x22
  <grplist>

  ;backproject initial volume
  @bpcg(x23) ;x23=maskradius
  <grptmpl>x22
  <dir>/rctangles
  <pcltmpl>
  <dir>/vol{***x22}

  ;refine volume by shift alignment of tilted particles
  @rctshiftrefine(x23,x31,x32,x41) ;radius,filt1,filt6,Apix
  <grptmpl>x22
  <pcltmpl>
  <dir>/rctangles
  <dir>/vol{***x22}  ;initial volume
  <dir>/vol{***x22}  ;directory by same name as unrefined volume

  ;overwrite initial volume with refined volume then remove refinement directory
  DE
  <dir>/vol{***x22}
  VM
  mv <dir>/vol{***x22}/vol006.$DATEXT <dir>/vol{***x22}.$DATEXT
  VM
  mv <dir>/vol{***x22}/shift006.$DATEXT <dir>/shiftdoc{***x22}.$DATEXT
  VM
  rm -fr <dir>/vol{***x22}/
LB1

;Combine anglesdoc and shiftdocs into single apsh format doc
@rct2apshdoc
<dir>/rctangles
<dir>/shiftdoc***
<grplist>
<dir>

DE
<dir>/rctangles

@deletelist
<dir>/shiftdoc***
<grplist>

GOTO LB90
VM
echo ____ Refining particle projection angles for {%F7.1%x21} volumes ____

x24=int(x20/3) ;alignradius

;MD
;set mp
;(1)
DO LB2 x12=1,x21 ;for each class
  UD IC x12,x22
  <grplist>
  
  @rctanglerefine(x24) ;alignradius
  <grptmpl>x22
  <pcltmpl>
  <dir>/rctapshdoc
  <dir>/vol{***x22}
  <dir>
  
  VM
  mv <dir>/rvol.$DATEXT <dir>/rvol{***x22}.$DATEXT
  VM
  mv <dir>/rctrefinedoc.$DATEXT <dir>/rctrefine{***x22}.$DATEXT
LB2
UD ICE
<grplist>

VM ;just concatenate to preserve key=pclnum
cat <dir>/rctrefine*.$DATEXT > <dir>/rctrefinedoc.$DATEXT

;DOC COMBINE ; doesn't preserve key=pclnum
;<dir>/rctrefine***
;<grplist>
;<dir>/rctrefinedoc

@deletelist
<dir>/rctrefine***
<grplist>
LB90

VM
echo ____ Calculating resolution of {%F7.1%x21} volumes ____
DO LB3 x13=1,x21 ;for each class
  UD IC x13,x22
  <grplist>
  
  VM
  echo -n Volume{***x22}'..'

  @fsc(x23,x43) ;(maskradius,tiltangle)
  <grptmpl>x22
  <initpcl>
  <dir>/rctapshdoc
  <dir>

  VM
  mv <dir>/dres.$DATEXT <dir>/dres{***x22}.$DATEXT

  @rescalc(x41,x25,x26) ;(Apix,0.5,3sig)
  <dir>/dres{***x22}
  <dir>/drescalc_tmp  ;appended to end of each round
LB3
UD ICE
<grplist>

DOC REN
<dir>/drescalc_tmp
<dir>/drescalc

DE
<dir>/drescalc_tmp

EN D
