; WindowAutoPick
; Description:
;   Checks to see which micros have autopic categorization files
;     =pauses to see if any micros were not categorized=
;   Cleans automatically selected particle lists
;    (score:good>bad>tail)
;   Windows raw particles

; ---- INPUT FILES ----
FR L    ;List of autopicked micros
<goodmics>pw/defocusgood

FR L    ;Directory of automatically selected particles
<picdir>autopic

FR L    ;Template for particle categorization docs in ./sel****/
<pictmpl>pic****

FR L    ;Tempate for micros
<mictmpl>mics/mic****

; ---- OUTPUT ----
FR L    ;Directory for windowed particles
<pcldir>pcl

; ---- PARAMETERS ----
RR x20  ;Box size in pixels
150

RR x21  ;Binning factor of particle coordinates
4

RR x29  ;Number of processors (0=All,<0=Unspecified)
0

; ~~~~~ start ~~~~~
IF(x29.ge.0)THEN
  MD
  set mp
  (x29)
ENDIF

x19=0
IQ FI x19
<picdir>/cleanlist
IF(x19.eq.1)THEN
  VM
  echo 'List of micros with categorized particles already exists: '<picdir>/cleanlist
  GOTO LB90
ENDIF

;--- Make a list of micros with categorized particles ---
@checkpics
<goodmics>
<picdir>
<pictmpl>
<picdir>/cleanlist

LB90 ;list of micros with categorized particles already exists

; --- Pause to check with user that all intended micros were categorized --- 
UD N x31
<picdir>/cleanlist
UD x31,x32
<picdir>/cleanlist
UD E
x19=0
IQ FI x19
<picdir>/goodcords{****x32}
IF(x19.eq.1)THEN
  VM
  echo Good coordinates have already been extracted from {%F7.1%x31} micros.
  GOTO LB91
ENDIF

IF(x29.ge.0)THEN
  MD
  set mp
  (1)
ENDIF

RR x19
?IN.Have you finished cleaning particles (0=No/Stop,1=Yes/Proceed)?
IF(x19.eq.0)GOTO LB99

IF(x29.ge.0)THEN
  MD
  set mp
  (x29)
ENDIF

;--- extract good particle coordinates  ---
@cleanautopic
<picdir>
<pictmpl>
<picdir>/cleanlist

LB91 ;good coordinates already extracted

;--- window particles ---
x19=0
IQ FI x19
<pcldir>/serlist
IF(x19.eq.1)THEN
  VM
  echo Particles already windowed in <pcldir>.
  GOTO LB92
ENDIF

@winpcls
<picdir>/cleanlist      ;List of micrographs (dir/microlist)
<mictmpl>               ;Template for micros (dir/mic****)
<picdir>/goodcords****  ;Template for particle coordinates (dir/coords****)
x21                     ;Binning of coordinates (1 if no decimation, #)
x20                     ;Box size
<pcldir>                ;Output directory (contains ser@ and serlist)

LB92 ;Particles already windowed
LB99 ;Haven't finished categorizing particles.
EN D
