(x20) ;alignradius
; Syntax: @rctanglerefine
; Description: 
;   project volume every 1 degree
;   projection match with 5 degree angular restriction
;   reset any inplane rotation greater than 5 degrees
; Output:
;   rctrefinedoc = apsh format particle transformation parameters
;   rvol = refined 3D reconstruction

FR
?IN.List of particles (dir/grp001)?<pcllist>
FR 
?IN.Particle template (dir/sert@*****)?<pcltmpl>
FR
?IN.APSH format angles doc (dir/rctapshdoc)?<angles>
FR
?IN.Initial volume (dir/vol)?<volin>
FR
?OUT.Existing directory for refined volume and transformation doc (dir)?<dir>

; ~~~~~ start ~~~~~
VM
echo -n Angular refinement of <volin>..

;---compare size of volume and particles---
FS
<volin>
FI x22
<volin>
(12)

DOC SORT
<pcllist>
<dir>/pcllistsrt_tmp
(1)
Y

UD N x30
<dir>/pcllistsrt_tmp
UD IC x30,x31
<dir>/pcllistsrt_tmp
UD ICE
<dir>/pcllistsrt_tmp
DE
<dir>/pcllistsrt_tmp

FS
<pcltmpl>x31
FI x23
<pcltmpl>x31
(12)

IF(x23.ne.x22)THEN
  VM
  echo ERROR..dimensions of volume are different than particle.
  EN
ENDIF

;---calculate translation range---
x24=int(x23/2-x20-3) ;max translation
IF(x24.lt.1)THEN ;radius is too large
  x24=int(x23/3) ;typical radius
  x20=int(x23/2-3) ;maxradius
  IF(x20.gt.x24)x20=x24 ;radius is lesser of x/3 or x/2-3
  x24=int(x23/2-x20-3) ;max translation
  IF(x24.lt.1)THEN
    VM
    echo ERROR: Cannot find a suitable translation range
    EN
  ENDIF
  VM
  echo Reset radius to {%F7.1%x20} pixels.
ENDIF

;---calculate inner ring---
x25=5
IF(x20.le.25)x25=3
IF(x20.le.10)x25=1

;---create angular reprojection doc---
x61=90 ;mintilt
x62=-90 ;maxtilt
DO LB2 x12=1,x30
  ;         psi,tta,phi,ref,pcl,crt,cxs,cys,npj,dif,cc ,rt ,xsh,ysh,mr
  UD IC x12,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
  <angles>
  IF(x42.lt.x61)x61=x42 ;set min tilt angle
  IF(x42.gt.x62)x62=x42 ;set max tilt angle
LB2
x61=int(x61-2) ;3degrees below min
x62=int(x62+2) ;3degrees above max

x99=0
IQ FI x99
<dir>/pjangles_tmp
IF(x99.eq.1)THEN
  DE
  <dir>/pjangles_tmp
ENDIF

x60=0
x21=0
DO LB3 x13=x61,x62 ;theta
  DO LB4 x14=1,120
    x63=(x14-1)*5 ;phi
    x21=x21+1
    SD x21,x60,x13,x63
    <dir>/pjangles_tmp
  LB4
LB3
SD E
<dir>/pjangles_tmp

;VO EA x21  ;report number of projections
;(1)       ;delta theta
;(x61,x62) ;range theta
;(0,359.9) ;range phi
;<dir>/pjangles_tmp

;VM
;tail <dir>/pjangles_tmp.$DATEXT

MS
_1@
x22,x22,(1)
x21

x26=x23/2-2 ;projection radius ~= image radius
PJ 3Q
<volin>
x26 ;radius of volume
(1-x21) ;selection of angles
<dir>/pjangles_tmp
_1@*****

VM
echo -n {%F7.1%x21} projections..
; between theta {%F7.1%x61} and {%F7.1%x62}..

x99=0
IQ FI x99
<dir>/rctrefinedoc
IF(x99.eq.1)THEN
  VM
  echo -n Removed prior apsh_doc.
  DE
  <dir>/rctrefinedoc
ENDIF

AP SH
_1@*****  ;reference tmpl
(1-x21)   ;reference list
x24,(1)   ;translation,step
x25,x20   ;inner,outer radius
<dir>/pjangles_tmp ;refangles
<pcltmpl> ;particle template
<pcllist> ;particle list
<angles>  ;particle alignment
(5,5)     ;anglerange,threshold
(0)       ;mirror
<dir>/rctrefinedoc     ;output alignment doc

DE
_1

DE
<dir>/pjangles_tmp

;---apply inplane rotation and shift---
SD IC NEW
rctrefine_ic
(15),x31

MS I
_2@
x22,x22,(1) ;size
x30         ;numpcl
x31         ;maxnum

x56=0 ;counter for inplane >5 particles
DO LB1 x11=1,x30 ;for each particle
  UD IC x11,x40
  <pcllist>
  ;         psi,tta,phi,ref,pcl,crt,cxs,cys,npj,dif,cc ,rt ,xsh,ysh,mr
  UD IC x40,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
  <dir>/rctrefinedoc
  
  IF(x40.ne.x45)THEN
    VM
    echo 'ERROR: '<dir>/rctrefinedoc
    VM
    echo '  particle number'{%F7.1%x40}' does not correspond with key number'{%F7.1%x40}
    EN
  ENDIF
  
  IF(x46.gt.5)THEN ;inplane rotation is greater than 5 degrees
    x56=x56+1
    UD IC x40,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
    <angles>
  ENDIF
  
  SD IC x40,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54,x55
  rctrefine_ic
  
  RT SQ
  <pcltmpl>x45
  _2@{******x45}
  x46
  x47,x48
LB1 ;next particle
UD ICE
<pcllist>

UD ICE
<dir>/rctrefinedoc

IF(x56.gt.0)THEN
  VM
  echo -n .{%F7.1%x56} of {%F7.1%x30} particles exceeded 5 degree rotation limit.
  UD ICE
  <angles>
ENDIF

SD IC COPY
rctrefine_ic
<dir>/rctrefinedoc
SD IC END
rctrefine_ic

VM
echo -n .Backproject.

BP 3F
_2@******   ;pcl template
<pcllist>   ;pcllist
<dir>/rctrefinedoc ;angles doc
*           ;no symmetry
<dir>/rvol  ;output volume

DE
_2

VM
echo .done.

RE
