[x20] ;alignradius
;rctanglerefine
;  project volume every 1 degree
;  projection match with 5 degree angular restriction
;  output is [dir]/apsh_doc and [dir]/rvol

FR
?IN.List of particles (dir/grp001)?[pcllist]
FR 
?IN.Particle template (dir/sert@*****)?[pcltmpl]
FR
?IN.APSH format angles doc (dir/rctangles)?[angles]
FR
?IN.Preliminary volume (dir/vol001)?[volin]
FR
?OUT.Existing directory for refined volume and new angles (dir)?[dir]

; ~~~~~ start ~~~~~
VM
echo -n Angular search for [volin]..

FS
[volin]
FI x22
[volin]
(12)

DOC SORT
[pcllist]
[dir]/pcllistsrt_tmp
(1)
Y
UD N x30
[dir]/pcllistsrt_tmp

UD IC x30,x31
[dir]/pcllistsrt_tmp
UD ICE
[dir]/pcllistsrt_tmp
DE
[dir]/pcllistsrt_tmp

FS
[pcltmpl]x31
FI x23
[pcltmpl]x31
(12)

IF(x23.ne.x22)THEN
  VM
  echo ERROR..dimensions of volume are different than particle
  EN
ENDIF

x24=x23/2-x20-2 ;translation range
IF(x24.lt.0)THEN ;radius is too large
  x24=0
  x20=x23/2-2
ENDIF
x25=5
IF(x20.le.25)x25=3
IF(x20.le.10)x25=1

x99=0
IQ FI x99
[dir]/pjangles_tmp
IF(x99.eq.1)THEN
  VM
  echo -n .Removing prior angles.
  DE
  [dir]/pjangles_tmp
ENDIF

VO EA
(2) ;delta theta
(0,90) ;range theta
(0,359.9) ; range phi
[dir]/pjangles_tmp

UD N x21
[dir]/pjangles_tmp
UD E

MS
_1@
x22,x22,(1)
x21

x26=x23/2-2 ;projection radius ~= image radius
PJ 3Q
[volin]
x26 ;radius of volume
(1-x21) ;selection of angles
[dir]/pjangles_tmp
_1@*****

x99=0
IQ FI x99
[dir]/apsh_doc
IF(x99.eq.1)THEN
  VM
  echo -n .Removing prior apsh_doc.
  DE
  [dir]/apsh_doc
ENDIF

AP SH
_1@*****  ;reference tmpl
(1-x21)   ;reference list
x24,(1)   ;translation,step
x25,x20   ;inner,outer radius
[dir]/pjangles_tmp ;refangles
[pcltmpl] ;particle template
[pcllist] ;particle list
[angles]  ;particle alignment
(5,2)     ;anglerange,threshold
(0)       ;mirror
[dir]/apsh_doc     ;output alignment doc
DOC REN
[dir]/apsh_doc
[dir]/apsh_ren

VM
echo -n .backproject..

;apply inplane rotation and shift
MS I
_2@
x22,x22,(1) ;size
x30         ;numpcl
x31         ;maxnum

GOTO LB98
;       psi,tta,phi,ref,pcl,crt,cxs,cys,npj,ang,rt ,xsh,ysh,mr
UD IC 1,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54
[dir]/apsh_ren
IF(x46.ne.x51)GOTO LB99
IF(x47.ne.x52)GOTO LB99
IF(x48.ne.x53)GOTO LB99
GOTO LB98
LB99
VM
echo .cumulative and current rotation and shift not equal.
LB98
DO LB1 x11=1,x30 ;for each particle
  ;         psi,tta,phi,ref,pcl,crt,cxs,cys,npj,ang,rt ,xsh,ysh,mr
  UD IC x11,x41,x42,x43,x44,x45,x46,x47,x48,x49,x50,x51,x52,x53,x54
  [dir]/apsh_ren

  RT SQ
  [pcltmpl]x45
  _2@{******x45}
  x46
  x47,x48
LB1 ;next particle
UD ICE
[dir]/apsh_ren
DE
[dir]/apsh_ren

BP 3F
_2@****** ;pcl template
[pcllist] ;pcllist
[dir]/apsh_doc ;angles doc
*         ;no symmetry
[dir]/rvol   ;output volume

DE
_1
DE
_2
DE
[dir]/pjangles_tmp

VM
echo .done.

RE

