; INTERACTIVE CORRESPONDANCE ANALYSIS & CLASSIFICATION
;
; 1) CORAN (CA S)
; 2) DISPLAY FACTOR HISTOGRAM AND FACTOR MAP (eigenhist.py)
; 3) GENERATE POSITIVE AND NEGATIVE EIGENIMAGES (CA SRE)
; 4) -PAUSES FOR INPUT NUMBER OF FACTORS AND WEIGHT
; 5) CLUSTERS WITH HIERARCHICAL ASCENDANT CLASSIFICATION (CL HC)
; 6) -PAUSES TO INPUT THRESHOLD
; 7) DETERMINES CLASS MEMBERSHIP (CL HD,CL HE) AND AVERAGES (AS R)
; 8) CLUSTERS WITH K-MEANS TO GENERATE SAME NUMBER OF CLASSES
; Ed Brignole 20081206

FR
?IN.Particles template to classify (dir/ser*****)?<pcltmpl1> 
FR
?IN.Particles template to average (dir/ser*****)?<pcltmpl2>
FR
?IN.List of particles (dir/doc)?<list>
FR
?IN.Mask (dir/mask)?<mask>
FR
?OUT.Directory to create (dir)?<dir>
FR
?OUT.Coran prefix (pfx, assumes _IMC)?<prefix>

; ~~~~~ start ~~~~~
MD
set mp
(0)

VM
echo Classifying into <dir>/<prefix>_

x99=0  ;check to see if this is repeat classification
IQ FI x99
<dir>/<prefix>_IMC
IF(x99.eq.1)THEN
  VM
  echo This is a repeat of an earlier classification..skipping coran.
  GOTO LB98
ENDIF

VM
mkdir <dir>
; ---------BEGIN CALLING SCRIPTS
x21=50 ; number of factors for analysis - will be overwritten later
; RUN CA S AND PLOT EIGENVECTOR HISTOGRAM
@coran(x21)
<pcltmpl1>          ; ALIGNED PARTICLES TEMPLATE
<list>           ; DOC LIST OF PARTICLES
<mask>             ; MASK FOR CLASSIFICATION
<dir>/<prefix>  ; OUTPUT PREFIX
; -------- MAKE HISTOGRAM
@eigenhist
<dir>/<prefix>
; -------- MAKE FACTOR MAP
UD N x23
<list>
UD E
IF(x23.gt.2000)THEN ; too many images to plot
  VM
  echo "...Skipping factor map, too many points (>2000)."
ELSE ;plot factor 1 vs 2
  x22 = 1
  x21 = 2
  @casm(x22,x21)
  <dir>/<prefix>            ; prefix of _IMC and _EIG from CA S
  VM
  ggv <dir>/<prefix>_map{**x22}v{**x21}.ps &
ENDIF
; ------- MAKE EIGENIMAGES
@casre
<dir>/<prefix>   ; input prefix
<mask>
; ------- CALCULATE DENDROGRAM
LB98 ;used prior coran

x99=0
IQ FI x99
<dir>/<prefix>_dendoc
IF(x99.eq.1)THEN
  DE
  <dir>/<prefix>_avg
  DE
  <dir>/<prefix>_var
  DE A
  <dir>/<prefix>_thr001
  DE
  <dir>/<prefix>_thrk
  DE A
  <dir>/<prefix>_km001
  DE
  <dir>/<prefix>_kmlist
  DE
  <dir>/<prefix>_kmav
  DE
  <dir>/<prefix>_kmva
  ;RR x99
  ;?IN.Prior dendrogram exits (use=1,recalculate=0)?
  ;IF(x99.eq.1)GOTO LB99
  DE
  <dir>/<prefix>_dendoc
ENDIF
MD
set mp
(1)

VM
echo Examine factor map, eigenimages, and eigenvalue histogram

x22=1 ;first factor
x24=0 ;weight
RR x21 ;nfact
?IN.DISCONTINUOUS FACTORS (0) or NUMBER OF CONTINUOUS FACTORS (>=1)?

MD
set mp
(0)
IF(x21.eq.0)THEN
  FR
  ?IN.Factors to keep (1,3-4,6)?<factors>
  @clhc_weight(x22,x21,x24) ;firstfactor,lastfactor,factorweight
  <dir>/<prefix> ;input prefix - output dendoc
  <factors>
ELSE ;continuous factors
  RR x24
  ?IN.Use factor variance as weight (yes=1,no=0,half=0.5,etc.)?
  @clhc_weight(x22,x21,x24) ;firstfactor,lastfactor,factorweight
  <dir>/<prefix> ;input prefix - output dendoc
ENDIF

; ------- INPUT THRESHOLD
LB99 ;use prior dendrogram
MD
set mp
(1)

VM
echo EXAMINE DENDROGRAM TO DETERMINE THRESHOLD
RR x25
?IN.Hierarchical Ascendant Threshold (0.02)?

MD
set mp
(0)
; ------- CLUSTER PARTICLES AND CALCULATE AVERAGES
@clust_thresh(x25)
<pcltmpl2>          ; ALIGNED PARTICLES TEMPLATE
<dir>/<prefix>   ; ASSUMES _dendoc FILE EXISTS

UD N x26 ;ngrps
<dir>/<prefix>_thrk
UD E
; ------- K-MEANS CLUSTERING
IF(x21.eq.0)THEN ; discontinuous
  @clust_kmeans(x21,x26) ;factors,groups
  <dir>/<prefix> ; ASSUMES <prefix>_IMC
  <factors>         ; discontinuous factors (optional if x21=0)
  <pcltmpl2>        ; ALIGNED PARTICLES TEMPLATE
ELSE ;continuous
  @clust_kmeans(x21,x26) ;factors,groups
  <dir>/<prefix> ; ASSUMES <prefix>_IMC
  <pcltmpl2>        ; ALIGNED PARTICLES TEMPLATE
ENDIF

; ------- CLEAN UP
DE
<dir>/<prefix>_PIX
DE
<dir>/<prefix>_SET
DE
<dir>/<prefix>_SEQ
DE
<dir>/<prefix>_MAS

MD
set mp
(1)
RE

